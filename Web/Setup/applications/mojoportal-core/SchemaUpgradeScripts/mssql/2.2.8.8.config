SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO




CREATE PROCEDURE [dbo].[mp_CommerceReport_GetSalesGroupedByUser]

/*
Author:   			
Created: 			2009-02-14
Last Modified: 		2009-02-14
*/

@SiteGuid uniqueidentifier

AS


SELECT TOP 20
c.UserGuid,
u.UserID,
COALESCE(u.[Name], 'deleted user') AS Name,
u.LoginName,
u.Email,

	SUM(c.SubTotal) As Sales
		
FROM
		[dbo].[mp_CommerceReport] c

LEFT OUTER JOIN [dbo].[mp_Users] u
ON	c.UserGuid = u.UserGuid
		
WHERE
		c.[SiteGuid] = @SiteGuid

GROUP BY 
c.UserGuid,
u.UserID,
u.[Name],
u.LoginName,
u.Email

ORDER BY SUM(SubTotal) desc

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[mp_CommerceReport_GetUserItemCountBySite]

/*
Author:   			
Created: 			2009-02-12
Last Modified: 		2009-02-12
*/

@SiteGuid uniqueidentifier

AS

SELECT COUNT(*) 

FROM 
(SELECT DISTINCT UserGuid
	FROM [dbo].[mp_CommerceReport]
	WHERE SiteGuid = @SiteGuid) a

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_CommerceReport_SelectUserItemPage]

-- Author:   			
-- Created: 			2009-02-14
-- Last Modified: 		2009-02-14

@SiteGuid uniqueidentifier,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
RowGuid UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
RowGuid
)

SELECT 
		c.[UserGuid]
		
FROM
		[dbo].[mp_CommerceReport] c

LEFT OUTER JOIN [dbo].[mp_Users] u
ON	c.UserGuid = u.UserGuid
		
WHERE c.SiteGuid = @SiteGuid

GROUP BY u.[Name], c.UserGuid

ORDER BY u.[Name]

END


SELECT

t1.UserGuid,
COALESCE(u.UserID, -1) AS UserID,
COALESCE(u.[Name], 'deleted user') AS [Name],
u.LoginName,
u.Email,
SUM(t1.SubTotal) AS Revenue
		
FROM
		[dbo].[mp_CommerceReport] t1

JOIN			#PageIndex t2
ON			
		t1.[UserGuid] = t2.[RowGuid]

LEFT OUTER JOIN [dbo].[mp_Users] u
ON	t1.UserGuid = u.UserGuid
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound

GROUP BY 
t1.UserGuid,
u.UserID,
u.[Name],
u.LoginName,
u.Email

		
ORDER BY u.[Name]

DROP TABLE #PageIndex


GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].mp_CommerceReport_SelectUserItemsPage

-- Author:   			
-- Created: 			2009-02-14
-- Last Modified: 		2009-02-14

@SiteGuid uniqueidentifier,
@UserGuid uniqueidentifier,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
RowGuid UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
RowGuid
)

SELECT
		[RowGuid]
		
FROM
		[dbo].[mp_CommerceReport]
		
WHERE	UserGuid = @UserGuid
		AND SiteGuid = @SiteGuid

ORDER BY OrderDateUtc DESC

END


SELECT
		t1.*
		
FROM
		[dbo].[mp_CommerceReport] t1

JOIN			#PageIndex t2
ON			
		t1.[RowGuid] = t2.[RowGuid]
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex

GO


SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[mp_CommerceReport_GetItemCountByUser]

/*
Author:   			
Created: 			2009-02-14
Last Modified: 		2009-02-14
*/

@SiteGuid uniqueidentifier,
@UserGuid uniqueidentifier

AS

SELECT COUNT(*) 
FROM [dbo].[mp_CommerceReport]

WHERE SiteGuid = @SiteGuid
AND UserGuid = @UserGuid

GO


SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

CREATE PROCEDURE [dbo].[mp_Users_UpdateTotalRevenueByUser]


@UserGuid	uniqueidentifier


AS

UPDATE mp_Users 
SET TotalRevenue = COALESCE((
SELECT SUM(SubTotal) FROM mp_CommerceReport WHERE UserGuid = @UserGuid)
, 0)

WHERE UserGuid = @UserGuid

GO



SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

CREATE PROCEDURE [dbo].[mp_Users_UpdateTotalRevenue]


AS

UPDATE mp_Users 
SET TotalRevenue = COALESCE((
SELECT SUM(SubTotal) FROM mp_CommerceReport WHERE UserGuid = mp_Users.UserGuid)
, 0)

GO


