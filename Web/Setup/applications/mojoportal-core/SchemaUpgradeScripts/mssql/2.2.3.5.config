SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_Modules_SelectPage]

/*
Author:			
Created:		5/16/2006
Last Modified:	5/20/2006

*/

@SiteID			int,
@PageNumber 	int,
@PageSize 		int,
@SortByModuleType	bit,
@SortByAuthor	bit



AS
DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
	ModuleID int
)	


 IF @SortByModuleType = 1
	BEGIN
	    	INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			mp_Modules m
			JOIN			mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			WHERE 			m.SiteID = @SiteID
							AND md.IsAdmin = 0
				
			ORDER BY 		md.FeatureName, m.ModuleTitle

	END
 Else IF @SortByAuthor = 1
	BEGIN
		INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			mp_Modules m
			JOIN			mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			LEFT OUTER JOIN	mp_Users u
			ON				m.CreatedByUserID = u.UserID
			WHERE 			m.SiteID = @SiteID
							AND md.IsAdmin = 0
				
			ORDER BY 		u.[Name], m.ModuleTitle
	END
 ELSE
	BEGIN
	    	INSERT INTO 	#PageIndex (ModuleID)

	    	SELECT 			m.ModuleID 
			FROM 			mp_Modules m
			JOIN			mp_ModuleDefinitions md
			ON				md.ModuleDefID = m.ModuleDefID
			WHERE 			m.SiteID = @SiteID
							AND md.IsAdmin = 0
				
			ORDER BY 		m.ModuleTitle

	END

DECLARE @TotalRows int
DECLARE @TotalPages int
DECLARE @Remainder int

SET @TotalRows = (SELECT Count(*) FROM #PageIndex)
SET @TotalPages = @TotalRows / @PageSize
SET @Remainder = @TotalRows % @PageSize
IF @Remainder > 0 
SET @TotalPages = @TotalPages + 1


SELECT		m.*,
			md.FeatureName,
			md.ControlSrc,
			md.ResourceFile,
			u.[Name] As CreatedBy,
			'TotalPages' = @TotalPages

FROM			mp_Modules m

JOIN			mp_ModuleDefinitions md
ON				md.ModuleDefID = m.ModuleDefID

LEFT OUTER JOIN	mp_Users u
ON				m.CreatedByUserID = u.UserID

JOIN			#PageIndex p
ON				m.ModuleID = p.ModuleID

WHERE 		
			p.IndexID > @PageLowerBound 
			AND p.IndexID < @PageUpperBound

ORDER BY		p.IndexID

DROP TABLE #PageIndex
GO
