ALTER TABLE [dbo].mp_ModuleDefinitionSettings ADD
	ControlSrc nvarchar(255) NULL 
GO

ALTER TABLE [dbo].mp_ModuleDefinitionSettings ADD
	SortOrder int NOT NULL DEFAULT 100
GO

ALTER TABLE [dbo].mp_ModuleDefinitionSettings ADD
	HelpKey nvarchar(255) NULL 
GO

ALTER TABLE [dbo].mp_ModuleSettings ADD
	ControlSrc nvarchar(255) NULL 
GO

ALTER TABLE [dbo].mp_ModuleSettings ADD
	SortOrder int NOT NULL DEFAULT 100
GO

ALTER TABLE [dbo].mp_ModuleSettings ADD
	HelpKey nvarchar(255) NULL 
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_Update]

@ModuleDefID      		int,
@SettingName   		nvarchar(50),
@SettingValue  			nvarchar(255),
@ControlType   			nvarchar(50),
@RegexValidationExpression 	ntext,
@FeatureGuid uniqueidentifier,
@ResourceFile nvarchar(255),
@ControlSrc	nvarchar(255),
@HelpKey	nvarchar(255),
@SortOrder	int

AS

IF NOT EXISTS (
    SELECT 
        * 
    FROM 
        mp_ModuleDefinitionSettings 
    WHERE 
        (ModuleDefID = @ModuleDefID OR FeatureGuid = @FeatureGuid)
      AND
        SettingName = @SettingName
)
INSERT INTO mp_ModuleDefinitionSettings (
	FeatureGuid,
    ModuleDefID,
	ResourceFile,
    SettingName,
    SettingValue,
	ControlType,
	RegexValidationExpression,
	ControlSrc,
	HelpKey,
	SortOrder
) 
VALUES (
	@FeatureGuid,
    @ModuleDefID,
	@ResourceFile,
    @SettingName,
    @SettingValue,
	@ControlType,
	@RegexValidationExpression,
	@ControlSrc,
	@HelpKey,
	@SortOrder
)
ELSE
UPDATE
    mp_ModuleDefinitionSettings

SET
	FeatureGuid = @FeatureGuid,
    SettingValue = @SettingValue,
	ControlType = @ControlType,
	RegexValidationExpression = @RegexValidationExpression,
	ResourceFile = @ResourceFile,
	ControlSrc = @ControlSrc,
	HelpKey = @HelpKey,
	SortOrder = @SortOrder

WHERE
    (ModuleDefID = @ModuleDefID OR FeatureGuid = @FeatureGuid)
  AND
    SettingName = @SettingName

GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_UpdateByID]

@ID						int,
@ModuleDefID      		int,
@SettingName   		nvarchar(50),
@SettingValue  			nvarchar(255),
@ControlType   			nvarchar(50),
@RegexValidationExpression 	ntext,
@ResourceFile nvarchar(255),
@ControlSrc	nvarchar(255),
@HelpKey	nvarchar(255),
@SortOrder	int

AS

UPDATE
    mp_ModuleDefinitionSettings

SET
    ResourceFile = @ResourceFile,
	SettingName = @SettingName,
    SettingValue = @SettingValue,
	ControlType = @ControlType,
	RegexValidationExpression = @RegexValidationExpression,
	ControlSrc = @ControlSrc,
	HelpKey = @HelpKey,
	SortOrder = @SortOrder

WHERE
    ID = @ID AND ModuleDefID = @ModuleDefID

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[mp_ModuleSettings_SyncDefinitions]

AS

UPDATE mp_ModuleSettings
SET ControlSrc = (SELECT TOP 1 mds.ControlSrc
				FROM mp_ModuleDefinitionSettings mds
				WHERE mds.ModuleDefId IN (SELECT ModuleDefId
										FROM mp_Modules m
										WHERE m.ModuleID = mp_ModuleSettings.ModuleID)
				AND mds.SettingName = mp_ModuleSettings.SettingName
				)

UPDATE mp_ModuleSettings
SET ControlType = (SELECT TOP 1 mds.ControlType
				FROM mp_ModuleDefinitionSettings mds
				WHERE mds.ModuleDefId IN (SELECT ModuleDefId
										FROM mp_Modules m
										WHERE m.ModuleID = mp_ModuleSettings.ModuleID)
				AND mds.SettingName = mp_ModuleSettings.SettingName
				)

UPDATE mp_ModuleSettings
SET SortOrder = (SELECT TOP 1 mds.SortOrder
				FROM mp_ModuleDefinitionSettings mds
				WHERE mds.ModuleDefId IN (SELECT ModuleDefId
										FROM mp_Modules m
										WHERE m.ModuleID = mp_ModuleSettings.ModuleID)
				AND mds.SettingName = mp_ModuleSettings.SettingName
				)
UPDATE    ms
SET    ms.RegexValidationExpression = mds.RegexValidationExpression
FROM    mp_ModuleSettings ms
    JOIN mp_Modules m
        ON ms.ModuleID = m.ModuleID
    JOIN mp_ModuleDefinitionSettings mds
        ON ms.SettingName = mds.SettingName
        AND m.ModuleDefId = mds.ModuleDefId

UPDATE mp_ModuleSettings
SET HelpKey = (SELECT TOP 1 mds.HelpKey
				FROM mp_ModuleDefinitionSettings mds
				WHERE mds.ModuleDefId IN (SELECT ModuleDefId
										FROM mp_Modules m
										WHERE m.ModuleID = mp_ModuleSettings.ModuleID)
				AND mds.SettingName = mp_ModuleSettings.SettingName
				)

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleSettings_Select]
(
    @ModuleID int
)
AS

DECLARE @ModuleDefID int
SELECT @ModuleDefID = ModuleDefID 
FROM mp_Modules 
WHERE ModuleID = @ModuleID

SELECT 	ms.*,
		mds.FeatureGuid,
		mds.ResourceFile

FROM		mp_ModuleSettings ms

JOIN		mp_ModuleDefinitionSettings mds
ON			mds.ModuleDefID = @ModuleDefID
			AND mds.SettingName = ms.SettingName

WHERE	ms.ModuleID = @ModuleID

ORDER BY ms.SortOrder

GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_Select]
(
    @ModuleDefID int
)
AS

SELECT
    *

FROM
    mp_ModuleDefinitionSettings

WHERE
    ModuleDefID = @ModuleDefID

ORDER BY SortOrder

GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER OFF
GO

ALTER PROCEDURE [dbo].[mp_ModuleSettings_CreateDefaultSettings]

/*
Author:			
Created:		1/1/2005
Last Modified:	2008-01-29

*/



@ModuleID		int


AS

INSERT INTO 	mp_ModuleSettings
(
			ModuleID,
			SettingName,
			SettingValue,
			ControlType,
			RegexValidationExpression,
			ModuleGuid,
			SettingGuid,
			ControlSrc,
			HelpKey,
			SortOrder
)

SELECT		m.ModuleID,
			ds.SettingName,
			ds.SettingValue,
			ds.ControlType,
			ds.RegexValidationExpression,
			m.[Guid],
			newid(),
			ds.ControlSrc,
			ds.HelpKey,
			ds.SortOrder


FROM			mp_Modules m

JOIN			mp_ModuleDefinitionSettings ds
ON			ds.ModuleDefID = m.ModuleDefID

WHERE		m.ModuleID = @ModuleID

ORDER BY		ds.[ID]

GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleSettings_Insert]

/*
Author:			
Created:		6/9/2005
Last Modified:		2008-06-04

*/


@SettingGuid		uniqueidentifier,
@ModuleGuid		uniqueidentifier,
@ModuleID			int,
@SettingName			nvarchar(50),
@SettingValue			nvarchar(255),
@ControlType			nvarchar(50),
@RegexValidationExpression 	ntext,
@ControlSrc			nvarchar(255),
@HelpKey			nvarchar(255),
@SortOrder			int




AS

INSERT INTO 	mp_ModuleSettings
(
			SettingGuid,
			ModuleGuid,
			ModuleID,
			SettingName,
			SettingValue,
			ControlType,
			RegexValidationExpression,
			ControlSrc,
			HelpKey,
			SortOrder
)

VALUES
(
			@SettingGuid,
			@ModuleGuid,
			@ModuleID,
			@SettingName,
			@SettingValue,
			@ControlType,
			@RegexValidationExpression,
			@ControlSrc,
			@HelpKey,
			@SortOrder
)

GO







