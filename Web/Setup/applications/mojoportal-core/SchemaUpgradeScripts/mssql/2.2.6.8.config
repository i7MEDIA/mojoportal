SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO



ALTER PROCEDURE [dbo].[mp_ForumPosts_SelectByThread]

/*
Author:				
Created:			2004-09-14
Last Modified:		2008-08-13

An approach to paging grids in the database, hopefully more efficient than bringing back a zillion rows and
paging with a DataGrid using viewstate

ThreadSequence is the integer order that postss were created within the thread

*/

@ThreadID			int,
@PageNumber			int

AS

DECLARE @PostsPerPage	int
--DECLARE @TotalPosts		int
DECLARE @CurrentPageMaxThreadSequence	int

DECLARE @BeginSequence int
DECLARE @EndSequence int



SELECT	@PostsPerPage = f.PostsPerPage
		--@TotalPosts = ft.TotalReplies

FROM		mp_ForumThreads ft

JOIN		mp_Forums f
ON		ft.ForumID = f.ItemID

WHERE	ft.ThreadID = @ThreadID

SET @CurrentPageMaxThreadSequence = (@PostsPerPage * @PageNumber) 
IF @CurrentPageMaxThreadSequence > @PostsPerPage
	BEGIN
		SET @BeginSequence = @CurrentPageMaxThreadSequence  - @PostsPerPage + 1
	END
ELSE
	BEGIN
		SET @BeginSequence = 1
	END

SET @EndSequence = @BeginSequence + @PostsPerPage  -1



SELECT	p.*,
		ft.ForumID,
		'MostRecentPostUser' = COALESCE(u.[Name],'Guest'),
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'AuthorEmail' = COALESCE(up.[Email], ''),
		'PostAuthorTotalPosts' = COALESCE(up.TotalPosts, 0),
		'Trusted' = COALESCE(up.Trusted, 0),
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		mp_ForumPosts p

JOIN		mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		AND p.ThreadSequence >= @BeginSequence
		AND  p.ThreadSequence <= @EndSequence

ORDER BY	p.SortOrder, p.PostID


GO


SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO


ALTER PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThreadRevereSorted]

/*
Author:				
Created:			2007-07-18
Last Modified:		2008-08-13


*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		'MostRecentPostUser' = COALESCE(u.[Name],'Guest'),
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'AuthorEmail' = COALESCE(up.[Email], ''),
		'PostAuthorTotalPosts' = COALESCE(up.TotalPosts, 0),
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		mp_ForumPosts p

JOIN		mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.ThreadSequence DESC 

GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[mp_BannedIPAddresses_Exists]

/*
Author:   			
Created: 			2008-08-13
Last Modified: 		2008-08-13
*/

@BannedIP nvarchar(50)

AS

SELECT COUNT(*) 
FROM [dbo].[mp_BannedIPAddresses]
WHERE BannedIP = @BannedIP

GO







