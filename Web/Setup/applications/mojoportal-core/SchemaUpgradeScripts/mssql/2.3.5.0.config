

ALTER TABLE [dbo].mp_ModuleDefinitionSettings ADD
	GroupName nvarchar(255) NULL 
GO

CREATE NONCLUSTERED INDEX [IX_mp_ModuleDefGroup] ON [dbo].[mp_ModuleDefinitionSettings] 
(
	[GroupName]
) WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF, DROP_EXISTING = OFF, ONLINE = OFF)
GO

UPDATE mp_SiteModuleDefinitions
SET AuthorizedRoles = 'All Users'

GO


SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_Update]

@ModuleDefID      		int,
@SettingName   		nvarchar(50),
@SettingValue  			nvarchar(max),
@ControlType   			nvarchar(50),
@RegexValidationExpression 	nvarchar(max),
@FeatureGuid uniqueidentifier,
@ResourceFile nvarchar(255),
@ControlSrc	nvarchar(255),
@HelpKey	nvarchar(255),
@SortOrder	int,
@GroupName nvarchar(255)

AS

IF NOT EXISTS (
    SELECT 
        * 
    FROM 
        mp_ModuleDefinitionSettings 
    WHERE 
        (ModuleDefID = @ModuleDefID OR FeatureGuid = @FeatureGuid)
      AND
        SettingName = @SettingName
)
INSERT INTO mp_ModuleDefinitionSettings (
	FeatureGuid,
    ModuleDefID,
	ResourceFile,
    SettingName,
    SettingValue,
	ControlType,
	RegexValidationExpression,
	ControlSrc,
	HelpKey,
	SortOrder,
	GroupName
) 
VALUES (
	@FeatureGuid,
    @ModuleDefID,
	@ResourceFile,
    @SettingName,
    @SettingValue,
	@ControlType,
	@RegexValidationExpression,
	@ControlSrc,
	@HelpKey,
	@SortOrder,
	@GroupName
)
ELSE
UPDATE
    mp_ModuleDefinitionSettings

SET
	FeatureGuid = @FeatureGuid,
    SettingValue = @SettingValue,
	ControlType = @ControlType,
	RegexValidationExpression = @RegexValidationExpression,
	ResourceFile = @ResourceFile,
	ControlSrc = @ControlSrc,
	HelpKey = @HelpKey,
	SortOrder = @SortOrder,
	GroupName = @GroupName

WHERE
    (ModuleDefID = @ModuleDefID OR FeatureGuid = @FeatureGuid)
  AND
    SettingName = @SettingName


GO





SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_Select]
(
    @ModuleDefID int
)
AS

SELECT
    *

FROM
    mp_ModuleDefinitionSettings

WHERE
    ModuleDefID = @ModuleDefID

ORDER BY SortOrder, GroupName


GO



ALTER PROCEDURE [dbo].[mp_ModuleSettings_Select]
(
    @ModuleID int
)
AS

DECLARE @ModuleDefID int
SELECT @ModuleDefID = ModuleDefID 
FROM mp_Modules 
WHERE ModuleID = @ModuleID

SELECT 	ms.ID,
		ms.ModuleID,
		ms.SettingName,
		ms.SettingValue,
		mds.ModuleDefID,
		mds.FeatureGuid,
		mds.ResourceFile,
		mds.ControlType,
		mds.RegexValidationExpression,
		mds.ControlSrc,
		mds.SortOrder,
		mds.HelpKey,
		mds.GroupName

FROM		mp_ModuleSettings ms

JOIN		mp_ModuleDefinitionSettings mds
ON			mds.ModuleDefID = @ModuleDefID
			AND mds.SettingName = ms.SettingName

WHERE	ms.ModuleID = @ModuleID

ORDER BY mds.SortOrder, mds.GroupName

GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_ModuleDefinitionSettings_UpdateByID]

@ID						int,
@ModuleDefID      		int,
@SettingName   		nvarchar(50),
@SettingValue  			nvarchar(max),
@ControlType   			nvarchar(50),
@RegexValidationExpression 	nvarchar(max),
@ResourceFile nvarchar(255),
@ControlSrc	nvarchar(255),
@HelpKey	nvarchar(255),
@SortOrder	int,
@GroupName nvarchar(255)

AS

UPDATE
    mp_ModuleDefinitionSettings

SET
    ResourceFile = @ResourceFile,
	SettingName = @SettingName,
    SettingValue = @SettingValue,
	ControlType = @ControlType,
	RegexValidationExpression = @RegexValidationExpression,
	ControlSrc = @ControlSrc,
	HelpKey = @HelpKey,
	SortOrder = @SortOrder,
	GroupName = @GroupName

WHERE
    ID = @ID AND ModuleDefID = @ModuleDefID


GO

ALTER PROCEDURE [dbo].[mp_SiteModuleDefinitions_EnsureForAdminSites]

/*
Author:			
Created:		2007-08-06
Last Modified:	2010-08-06

*/


AS

INSERT INTO mp_SiteModuleDefinitions
(
	SiteID,
	ModuleDefID,
	SiteGuid,
	FeatureGuid,
	AuthorizedRoles
)

SELECT
	s.SiteID,
	md.ModuleDefID,
	s.SiteGuid,
	md.[Guid],
	'All Users'

FROM
	mp_Sites s

CROSS JOIN
	mp_ModuleDefinitions md

WHERE	s.IsServerAdminSite = 1
AND md.Guid NOT IN
(SELECT sd.FeatureGuid FROM mp_SiteModuleDefinitions sd
	WHERE sd.SiteGuid = s.SiteGuid)


GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_SiteModuleDefinitions_Insert]

/*
Author:			
Created:		3/12/2005
Last Modified:	2010-08-06

*/


@SiteGuid		uniqueidentifier,
@FeatureGuid	uniqueidentifier



AS

DECLARE @SiteID	int
DECLARE @ModuleDefID int

SET @SiteID = (SELECT TOP 1 SiteID FROM mp_Sites WHERE SiteGuid = @SiteGuid)
SET @ModuleDefID = (SELECT TOP 1 ModuleDefID FROM mp_ModuleDefinitions WHERE Guid = @FeatureGuid)

IF NOT EXISTS (SELECT SiteID FROM mp_SiteModuleDefinitions WHERE SiteID = @SiteID AND ModuleDefID = @ModuleDefID)
INSERT INTO mp_SiteModuleDefinitions (SiteID, ModuleDefID, SiteGuid, FeatureGuid, AuthorizedRoles)

VALUES	(@SiteID, @ModuleDefID, @SiteGuid, @FeatureGuid, 'All Users')


GO

CREATE PROCEDURE [dbo].[mp_SiteModuleDefinitions_Update]

/*
Author:   			
Created: 			2010-08-06
Last Modified: 		2010-08-06

*/

@SiteID int,
@ModuleDefID int,
@AuthorizedRoles nvarchar(max)
	
AS

UPDATE mp_SiteModuleDefinitions
SET AuthorizedRoles = @AuthorizedRoles
WHERE SiteID = @SiteID AND ModuleDefID = @ModuleDefID


GO


