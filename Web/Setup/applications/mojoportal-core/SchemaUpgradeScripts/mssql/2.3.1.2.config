SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER OFF
GO


ALTER PROCEDURE [dbo].[mp_PageModule_SelectByModule]

/*
Author:			
Created:		2007-07-23
Last Modified:	2009-07-19

*/
    
@ModuleID  	int


AS

SELECT pm.*,
m.ModuleTitle,
p.PageName,
p.UseUrl,
p.Url
		
FROM		[dbo].mp_PageModules pm

JOIN		[dbo].mp_Modules m
ON			pm.ModuleID = m.ModuleID

JOIN		[dbo].mp_Pages p
ON			pm.PageID = p.PageID
		
WHERE	pm.ModuleID = @ModuleID

GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER Procedure [dbo].[mp_ContentWorkflow_SelectWorkInProgress]

/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/

@ModuleGuid uniqueidentifier
As

SELECT	cw.*,
		m.ModuleTitle,
		m.ModuleID,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail		
				
FROM
		[dbo].[mp_ContentWorkflow] cw
		
JOIN    [dbo].mp_Modules m
ON
		cw.ModuleGuid = m.[Guid]
		
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
		
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.[Guid]
AND		cwah.Active = 1		

LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid
		
WHERE
		cw.[ModuleGuid] = @ModuleGuid
AND		 cw.[Status] Not In ('Cancelled','Approved')
		
ORDER BY
		cw.CreatedDateUtc DESC


GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO




ALTER Procedure [dbo].[mp_ContentWorkflow_SelectWorkInProgressByStatus]

/*
Author:   			
Created: 			2009-07-18
Last Modified: 		2009-07-19
*/

@ModuleGuid uniqueidentifier,
@Status nvarchar(20)

As

SELECT	cw.*,
		m.ModuleID,
		m.ModuleTitle,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail		
				
FROM
		[dbo].[mp_ContentWorkflow] cw
		
JOIN    [dbo].mp_Modules m
ON
		cw.ModuleGuid = m.[Guid]
		
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
		
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.[Guid]
AND		cwah.Active = 1		

LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid
		
WHERE
		cw.[ModuleGuid] = @ModuleGuid
AND		 cw.[Status] = @Status
		
ORDER BY
		cw.CreatedDateUtc DESC



GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER Procedure [dbo].[mp_ContentWorkflow_SelectPage]


/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-07-19
*/

@SiteGuid	uniqueidentifier,
@Status		nvarchar(20),
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
[Guid] UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		[Guid]
		
FROM
		[dbo].[mp_ContentWorkflow]		
WHERE
		SiteGuid = @SiteGuid
AND		[Status] = @Status

ORDER BY
		CreatedDateUtc DESC

END


SELECT
		cw.*,
		m.ModuleID,
		m.ModuleTitle,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail	
		
FROM
		[dbo].[mp_ContentWorkflow] cw

JOIN	#PageIndex idx
ON			
		cw.[Guid] = idx.[Guid]
JOIN    [dbo].mp_Modules m
ON
		cw.ModuleGuid = m.[Guid]
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.Guid
AND		cwah.Active = 1
LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid		
WHERE
		idx.IndexID > @PageLowerBound 
		AND idx.IndexID < @PageUpperBound
		
ORDER BY idx.IndexID

DROP TABLE #PageIndex


GO

SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER OFF
GO


ALTER PROCEDURE [dbo].[mp_PageModule_SelectByPage]

/*
Author:			
Created:		2007-09-01
Last Modified:	2009-07-19

*/
    
@PageID  	int


AS

SELECT pm.*,
m.ModuleTitle,
p.PageName,
p.UseUrl,
p.Url
		
FROM		[dbo].mp_PageModules pm

JOIN		[dbo].mp_Modules m
ON			pm.ModuleID = m.ModuleID

JOIN		[dbo].mp_Pages p
ON			pm.PageID = p.PageID
		
WHERE	pm.PageID = @PageID

GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE PROCEDURE [dbo].[mp_ContentWorkflow_SelectPageInfoForPage]


/*
Author:   			
Created: 			2009-07-20
Last Modified: 		2009-07-20
*/

@SiteGuid	uniqueidentifier,
@Status		nvarchar(20),
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
[Guid] UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		[Guid]
		
FROM
		[dbo].[mp_ContentWorkflow]		
WHERE
		SiteGuid = @SiteGuid
AND		[Status] = @Status

ORDER BY
		CreatedDateUtc DESC

END


SELECT
		p.PageID,
		p.PageGuid,
		p.PageName,
		p.UseUrl,
		p.Url As PageUrl,
		cw.[Guid] As WorkflowGuid
		
		
FROM
		[dbo].[mp_ContentWorkflow] cw

JOIN	#PageIndex idx
ON			
		cw.[Guid] = idx.[Guid]
		
JOIN    [dbo].mp_Modules m
ON
		cw.ModuleGuid = m.[Guid]
		
JOIN
		[dbo].[mp_PageModules] pm
ON
		pm.ModuleID = m.ModuleID
		
JOIN
		[dbo].[mp_Pages] p
ON	
		pm.PageID = p.PageID


WHERE
		idx.IndexID > @PageLowerBound 
		AND idx.IndexID < @PageUpperBound
		
ORDER BY idx.IndexID

DROP TABLE #PageIndex



GO

SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER OFF
GO



ALTER PROCEDURE [dbo].[mp_Pages_SelectPendingPageListPage]


/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-07-20
*/

@SiteGuid	uniqueidentifier,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
[Guid] UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT	[PageGuid]		
FROM	[dbo].[mp_Pages]		
WHERE	SiteGuid = @SiteGuid
--AND		IsPending = 1
AND PageID IN (SELECT pm.PageID FROM	[dbo].mp_PageModules pm
		JOIN [dbo].mp_ContentWorkflow cw
			ON cw.ModuleGuid = pm.ModuleGuid			
		WHERE	[Status] Not In ('Cancelled','Approved'))
ORDER BY
		PageName 

END

SELECT  p.*,
		wip.Total as WipCount    
FROM    [dbo].mp_Pages p
JOIN	#PageIndex idx
ON		p.[PageGuid] = idx.[Guid]
INNER JOIN			
	(	SELECT	Count(*) as Total,
				pm.PageGuid
		FROM	[dbo].mp_PageModules pm
		INNER JOIN [dbo].mp_ContentWorkflow cw
			ON cw.ModuleGuid = pm.ModuleGuid			
		WHERE	[Status] Not In ('Cancelled','Approved') 
		GROUP BY pm.PageGuid ) as wip
	ON	wip.PageGuid = p.PageGuid
WHERE
		idx.IndexID > @PageLowerBound 
		AND idx.IndexID < @PageUpperBound
ORDER BY p.PageName



GO

SET ANSI_NULLS OFF
GO

SET QUOTED_IDENTIFIER OFF
GO

ALTER Procedure [dbo].[mp_Pages_SelectPendingPageCount]

/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-07-20
*/

@SiteGuid uniqueidentifier

As

Select	Count(*)
From	[dbo].mp_Pages
Where	
	SiteGuid = @SiteGuid
	AND PageID IN (SELECT pm.PageID FROM	[dbo].mp_PageModules pm
		JOIN [dbo].mp_ContentWorkflow cw
			ON cw.ModuleGuid = pm.ModuleGuid			
		WHERE	[Status] Not In ('Cancelled','Approved'))
	
GO

