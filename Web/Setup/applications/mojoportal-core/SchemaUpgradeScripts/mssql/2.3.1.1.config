INSERT INTO [dbo].[mp_SiteSettingsExDef]([KeyName],[DefaultValue],[GroupName],[SortOrder]) 
VALUES('EnableContentWorkflow','false','ContentWorkflow', 100)

INSERT INTO [dbo].[mp_SiteSettingsExDef]([KeyName],[DefaultValue],[GroupName],[SortOrder]) 
VALUES('SiteRootDraftEditRoles','Content Authors;','Admin', 100)

ALTER TABLE [dbo].mp_Modules ADD
	DraftEditRoles ntext NULL 
	
ALTER TABLE [dbo].mp_Pages ADD
	DraftEditRoles ntext NULL 
	
ALTER TABLE [dbo].mp_Pages ADD
	IsPending bit default 0 not null 
	
	
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ContentWorkflow](
	[Guid] [uniqueidentifier] NOT NULL,
	[SiteGuid] [uniqueidentifier] NOT NULL,
	[ModuleGuid] [uniqueidentifier] NOT NULL,
	[CreatedDateUtc] [datetime] NOT NULL,
	[UserGuid] [uniqueidentifier] NOT NULL,
	[LastModUserGuid] [uniqueidentifier] NULL,
	[LastModUtc] [datetime] NULL,
	[Status] [nvarchar](20) NOT NULL,
	[ContentText] [ntext] NULL,
	[CustomData] [ntext] NULL,
	[CustomReferenceNumber] [int] NULL,
	[CustomReferenceGuid] [uniqueidentifier] NULL,
 CONSTRAINT [PK_mp_ContentWorkflow] PRIMARY KEY CLUSTERED 
(
	[Guid] ASC
) ON [PRIMARY]
) ON [PRIMARY] TEXTIMAGE_ON [PRIMARY]
GO
CREATE NONCLUSTERED INDEX [IX_mp_ContentWorkflow] ON [dbo].[mp_ContentWorkflow] 
(
	[SiteGuid] ASC
) ON [PRIMARY]
GO
CREATE NONCLUSTERED INDEX [IX_mp_ContentWorkflow_1] ON [dbo].[mp_ContentWorkflow] 
(
	[ModuleGuid] ASC
) ON [PRIMARY]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
Create Procedure [dbo].[mp_ContentWorkflow_SelectPage]


/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/

@SiteGuid	uniqueidentifier,
@Status		nvarchar(20),
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
[Guid] UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT
		[Guid]
		
FROM
		[dbo].[mp_ContentWorkflow]		
WHERE
		SiteGuid = @SiteGuid
AND		[Status] = @Status

ORDER BY
		CreatedDateUtc DESC

END


SELECT
		cw.*,
		m.ModuleTitle,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail	
		
FROM
		[dbo].[mp_ContentWorkflow] cw

JOIN	#PageIndex idx
ON			
		cw.[Guid] = idx.[Guid]
JOIN    [dbo].mp_Modules m
ON
		cw.ModuleGuid = m.Guid
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.[Guid]
AND		cwah.Active = 1
LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid		
WHERE
		idx.IndexID > @PageLowerBound 
		AND idx.IndexID < @PageUpperBound
		
ORDER BY idx.IndexID

DROP TABLE #PageIndex
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ContentWorkflowAuditHistory](
	[Guid] [uniqueidentifier] NOT NULL,
	[ContentWorkflowGuid] [uniqueidentifier] NOT NULL,
	[ModuleGuid] [uniqueidentifier] NOT NULL,
	[UserGuid] [uniqueidentifier] NOT NULL,
	[CreatedDateUtc] [datetime] NOT NULL,
	[NewStatus] [nvarchar](20) NULL,
	[Notes] [ntext] NULL,
	[Active] [bit] NULL,
 CONSTRAINT [PK_mp_ContentWorkflowAuditHistory] PRIMARY KEY CLUSTERED 
(
	[Guid] ASC
) ON [PRIMARY]
) ON [PRIMARY]
GO
CREATE NONCLUSTERED INDEX [IX_mp_ContentWorkflowAuditHistory] ON [dbo].[mp_ContentWorkflowAuditHistory] 
(
	[ContentWorkflowGuid] ASC
) ON [PRIMARY]
GO
CREATE NONCLUSTERED INDEX [IX_mp_ContentWorkflowAuditHistory_1] ON [dbo].[mp_ContentWorkflowAuditHistory] 
(
	[ModuleGuid] ASC
) ON [PRIMARY]
GO
CREATE NONCLUSTERED INDEX [IX_mp_ContentWorkflowAuditHistory_2] ON [dbo].[mp_ContentWorkflowAuditHistory] 
(
	[UserGuid] ASC
) ON [PRIMARY]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
Create Procedure [dbo].[mp_ContentWorkflow_GetInProgressCountByPage]
/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/
@PageGuid uniqueidentifier

As

Select	Count(*)
From	[dbo].mp_ContentWorkflow cw
Inner Join [dbo].mp_PageModules pm
On pm.ModuleGuid = cw.ModuleGuid
Where	pm.PageGuid = @PageGuid
AND		 cw.[Status] Not In ('Cancelled','Approved')
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_ContentWorkflow_Insert]

/*
Author:   			Kevin Needham
Created: 			2009-06-15
Last Modified: 		2009-06-15
*/

@Guid uniqueidentifier,
@SiteGuid uniqueidentifier,
@ModuleGuid uniqueidentifier,
@CreatedDateUtc datetime,
@UserGuid uniqueidentifier,
@Status nvarchar(20),
@ContentText ntext,
@CustomData ntext,
@CustomReferenceNumber int,
@CustomReferenceGuid  uniqueidentifier

	
AS

	INSERT INTO 	[dbo].[mp_ContentWorkflow] 
	(
					[Guid],
					[SiteGuid],					
					[ModuleGuid],
					[CreatedDateUtc],
					[UserGuid],
					[LastModUserGuid],
					[LastModUtc],
					[Status],
					[ContentText],
					[CustomData],
					[CustomReferenceNumber],
					[CustomReferenceGuid]
					
	) 

	VALUES 
	(
					@Guid,
					@SiteGuid,
					@ModuleGuid,
					@CreatedDateUtc,
					@UserGuid,
					@UserGuid,
					@CreatedDateUtc,
					@Status,
					@ContentText,
					@CustomData,
					@CustomReferenceNumber,
					@CustomReferenceGuid
					
	)

	
	

GO



SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
Create Procedure [dbo].[mp_ContentWorkflow_GetCount]
/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/
@SiteGuid uniqueidentifier,
@Status nvarchar(20)

As

Select	Count(*)
From	[dbo].mp_ContentWorkflow
Where	SiteGuid = @SiteGuid
And		[Status] = @Status
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
Create Procedure [dbo].[mp_ContentWorkflow_SelectWorkInProgress]

/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/

@ModuleGuid uniqueidentifier
As

SELECT	cw.*,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail		
				
FROM
		[dbo].[mp_ContentWorkflow] cw
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.[Guid]
AND		cwah.Active = 1		
LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid
WHERE
		cw.[ModuleGuid] = @ModuleGuid
AND		 [Status] Not In ('Cancelled','Approved')
		
ORDER BY
		cw.CreatedDateUtc DESC
GO


SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[mp_ContentWorkflow_Update]

/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-07-18
*/

@Guid uniqueidentifier,
@LastModUserGuid uniqueidentifier,
@LastModUtc datetime,
@Status nvarchar(20),
@ContentText ntext,
@CustomData ntext,
@CustomReferenceNumber int,
@CustomReferenceGuid  uniqueidentifier

	
AS

	UPDATE	[dbo].[mp_ContentWorkflow] 
	SET		
			[LastModUserGuid] = @LastModUserGuid,
			[LastModUtc] = @LastModUtc,
			[Status] = @Status,
			[ContentText] = @ContentText,
			[CustomData] = @CustomData,			
			[CustomReferenceNumber] = @CustomReferenceNumber,
			[CustomReferenceGuid] = @CustomReferenceGuid
			
	WHERE 	[Guid] = @Guid



GO





ALTER TABLE [dbo].[mp_ContentWorkflow] ADD  DEFAULT (newid()) FOR [Guid]
GO

ALTER TABLE [dbo].[mp_ContentWorkflowAuditHistory] ADD  CONSTRAINT [DF__mp_Content__Guid__186270A4]  DEFAULT (newid()) FOR [Guid]
GO

ALTER TABLE [dbo].[mp_ContentWorkflowAuditHistory]  WITH CHECK ADD  CONSTRAINT [FK_mp_ContentWorkflowAuditHistory_mp_ContentWorkflow] FOREIGN KEY([ContentWorkflowGuid])
REFERENCES [dbo].[mp_ContentWorkflow] ([Guid])
GO
ALTER TABLE [dbo].[mp_ContentWorkflowAuditHistory] CHECK CONSTRAINT [FK_mp_ContentWorkflowAuditHistory_mp_ContentWorkflow]
GO



SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_Modules_Update]

/*
Author:   			
Created: 			2004-12-26
Last Modified: 		2009-06-20

*/
	
@ModuleID int, 
@ModuleDefID int, 
@ModuleTitle nvarchar(255), 
@AuthorizedEditRoles ntext, 
@DraftEditRoles ntext,
@CacheTime int, 
@ShowTitle bit ,
@EditUserID	int,
@AvailableForMyPage	bit,
@AllowMultipleInstancesOnMyPage	bit,
@Icon	nvarchar(255),
@HideFromAuth bit,
@HideFromUnAuth bit,
@ViewRoles ntext


AS
UPDATE 		[dbo].[mp_Modules] 

SET
			
			[ModuleDefID] = @ModuleDefID,
			
			[ModuleTitle] = @ModuleTitle,
			[AuthorizedEditRoles] = @AuthorizedEditRoles,
			[DraftEditRoles] = @DraftEditRoles,
			[CacheTime] = @CacheTime,
			[ShowTitle] = @ShowTitle,
			EditUserID = @EditUserID,
			AvailableForMyPage = @AvailableForMyPage,
			AllowMultipleInstancesOnMyPage = @AllowMultipleInstancesOnMyPage,
			Icon = @Icon,
			HideFromAuth = @HideFromAuth,
			HideFromUnAuth = @HideFromUnAuth,
			ViewRoles = @ViewRoles
			
WHERE
			[ModuleID] = @ModuleID


SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[mp_Modules_Insert]

/*
Author:   			
Created: 			2004-12-26
Last Modified: 		2009-06-20

*/

@PageID int,
@SiteID		int,
@ModuleDefID int,
@ModuleOrder int,
@PaneName nvarchar(50),
@ModuleTitle nvarchar(255),
@AuthorizedEditRoles ntext,
@DraftEditRoles ntext,
@CacheTime int,
@ShowTitle bit,
@AvailableForMyPage	bit,
@CreatedByUserID	int,
@CreatedDate		datetime,
@AllowMultipleInstancesOnMyPage	bit,
@Icon	nvarchar(255),
@Guid	uniqueidentifier,
@FeatureGuid uniqueidentifier,
@SiteGuid	uniqueidentifier,
@HideFromAuth bit,
@HideFromUnAuth bit,
@ViewRoles ntext

	
AS
DECLARE @ModuleID int

INSERT INTO 	[dbo].[mp_Modules] 
(
				SiteID,
				SiteGuid,
				[ModuleDefID],
				[ModuleTitle],
				[AuthorizedEditRoles],
				[DraftEditRoles],
				[CacheTime],
				[ShowTitle],
				AvailableForMyPage,
				AllowMultipleInstancesOnMyPage,
				Icon,
				CreatedByUserID,
				CreatedDate,
				[Guid],
				FeatureGuid,
				HideFromAuth,
				HideFromUnAuth,
				ViewRoles
) 

VALUES 
(
				@SiteID,
				@SiteGuid,
				@ModuleDefID,
				@ModuleTitle,
				@AuthorizedEditRoles,
				@DraftEditRoles,
				@CacheTime,
				@ShowTitle,
				@AvailableForMyPage,
				@AllowMultipleInstancesOnMyPage,
				@Icon,
				@CreatedByUserID,
				@CreatedDate,
				@Guid,
				@FeatureGuid,
				@HideFromAuth,
				@HideFromUnAuth,
				@ViewRoles
				
)
SELECT @ModuleID =  @@IDENTITY

IF @PageID > -1
BEGIN

DECLARE @PageGuid uniqueidentifier
SET @PageGuid = (SELECT TOP 1 PageGuid FROM mp_Pages WHERE PageID = @PageID)

INSERT INTO 	[dbo].[mp_PageModules] 
(
				[PageID],
				[ModuleID],
				[ModuleOrder],
				[PaneName],
				[PublishBeginDate],
				PageGuid,
				ModuleGuid
				
) 

VALUES 
(
				@PageID,
				@ModuleID,
				@ModuleOrder,
				@PaneName,
				@CreatedDate,
				@PageGuid,
				@Guid
				
				
)
END


SELECT @ModuleID
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_Pages_Insert]

/*
Author:			
Created:		2004-11-17
Last Modified:	2009-06-20

*/

@SiteID   		int,
@ParentID		int,
@PageName    		nvarchar(255),
@PageOrder   		int,
@AuthorizedRoles 	ntext,
@EditRoles		ntext,
@DraftEditRoles ntext,
@CreateChildPageRoles ntext,
@RequireSSL		bit,
@ShowBreadcrumbs 	bit,
@ShowChildPageBreadcrumbs 	bit,
@PageKeyWords	nvarchar(255),
@PageDescription	nvarchar(255),
@PageEncoding	nvarchar(255),
@AdditionalMetaTags	nvarchar(255),
@UseUrl		bit,
@Url			nvarchar(255),
@OpenInNewWindow	bit,
@ShowChildPageMenu	bit,
@HideMainMenu	bit,
@Skin			nvarchar(100),
@IncludeInMenu	bit,
@MenuImage			nvarchar(50),
@PageTitle    		nvarchar(255),
@AllowBrowserCache	bit,
@ChangeFrequency	nvarchar(20),
@SiteMapPriority			nvarchar(10),
@LastModifiedUTC			datetime,
@PageGuid	uniqueidentifier,
@ParentGuid uniqueidentifier,
@HideAfterLogin	bit,
@SiteGuid	uniqueidentifier,
@CompiledMeta		ntext,
@CompiledMetaUtc	datetime,
@IncludeInSiteMap bit,
@IsClickable bit,
@ShowHomeCrumb bit,
@IsPending bit

AS
INSERT INTO 		mp_Pages
(
    			SiteID,
			ParentID,
    			PageName,
				PageTitle,
    			PageOrder,
			AuthorizedRoles,
			EditRoles,
			DraftEditRoles,
			CreateChildPageRoles,
    			RequireSSL,
			AllowBrowserCache,
    			ShowBreadcrumbs,
			ShowChildBreadCrumbs,
    			PageKeyWords,
			PageDescription,
			PageEncoding,
			AdditionalMetaTags,
			UseUrl,
			Url,
			OpenInNewWindow,
			ShowChildPageMenu,
			HideMainMenu,
			Skin,
			IncludeInMenu,
			MenuImage,
			ChangeFrequency,
			SiteMapPriority,
			LastModifiedUTC,
			PageGuid,
			ParentGuid,
			HideAfterLogin,
			SiteGuid,
			CompiledMeta,
			CompiledMetaUtc,
			IncludeInSiteMap,
			IsClickable,
			ShowHomeCrumb,
			IsPending
)

VALUES
(
    			@SiteID,
			@ParentID,
    			@PageName,
				@PageTitle,
    			@PageOrder,
			@AuthorizedRoles,
			@EditRoles,
			@DraftEditRoles,
			@CreateChildPageRoles,
    			@RequireSSL,
			@AllowBrowserCache,
    			@ShowBreadcrumbs,
			@ShowChildPageBreadcrumbs,
			@PageKeyWords,
			@PageDescription,
			@PageEncoding,
			@AdditionalMetaTags,
			@UseUrl,
			@Url,
			@OpenInNewWindow,
			@ShowChildPageMenu,
			@HideMainMenu,
			@Skin,
			@IncludeInMenu,
			@MenuImage,
			@ChangeFrequency,
			@SiteMapPriority,
			@LastModifiedUTC,
			@PageGuid,
			@ParentGuid,
			@HideAfterLogin,
			@SiteGuid,
			@CompiledMeta,
			@CompiledMetaUtc,
			@IncludeInSiteMap,
			@IsClickable,
			@ShowHomeCrumb,
			@IsPending
)

SELECT  @@Identity As PageID

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_Pages_Update]

/*
Author:			
Last Modified:		2009-06-20

*/


@SiteID        		int,
@PageID           	int,
@ParentID		int,
@PageOrder        	int,
@PageName         	nvarchar(255),
@AuthorizedRoles 	ntext,
@EditRoles		ntext,
@DraftEditRoles		ntext,
@CreateChildPageRoles ntext,
@RequireSSL		bit,
@ShowBreadcrumbs	bit,
@ShowChildPageBreadcrumbs bit,
@PageKeyWords	nvarchar(255),
@PageDescription	nvarchar(255),
@PageEncoding	nvarchar(255),
@AdditionalMetaTags	nvarchar(255),
@UseUrl		bit,
@Url			nvarchar(255),
@OpenInNewWindow	bit,
@ShowChildPageMenu	bit,
@HideMainMenu	bit,
@Skin			nvarchar(100),
@IncludeInMenu	bit,
@MenuImage			nvarchar(50),
@PageTitle         	nvarchar(255),
@AllowBrowserCache	bit,
@ChangeFrequency	nvarchar(20),
@SiteMapPriority			nvarchar(10),
@LastModifiedUTC			datetime,
@ParentGuid uniqueidentifier,
@HideAfterLogin bit,
@CompiledMeta		ntext,
@CompiledMetaUtc	datetime,
@IncludeInSiteMap bit,
@IsClickable bit,
@ShowHomeCrumb bit,
@IsPending bit


AS
UPDATE
    mp_Pages

SET
	ParentID = @ParentID,
    	PageOrder = @PageOrder,
    	PageName = @PageName,
		PageTitle = @PageTitle,
    	AuthorizedRoles = @AuthorizedRoles,
	EditRoles = @EditRoles,
	DraftEditRoles = @DraftEditRoles,
	CreateChildPageRoles = @CreateChildPageRoles,
    	RequireSSL = @RequireSSL,
	AllowBrowserCache = @AllowBrowserCache,
	ShowBreadcrumbs = @ShowBreadcrumbs,
	ShowChildBreadCrumbs = @ShowChildPageBreadcrumbs,
	PageKeyWords = @PageKeyWords,
	PageDescription = @PageDescription,
	PageEncoding = @PageEncoding,
	AdditionalMetaTags = @AdditionalMetaTags,
	UseUrl = @UseUrl,
	Url = @Url,
	OpenInNewWindow = @OpenInNewWindow,
	ShowChildPageMenu = @ShowChildPageMenu,
	HideMainMenu = @HideMainMenu,
	Skin = @Skin,
	IncludeInMenu = @IncludeInMenu,
	MenuImage = @MenuImage,
	ChangeFrequency = @ChangeFrequency,
	SiteMapPriority = @SiteMapPriority,
	LastModifiedUTC = @LastModifiedUTC,
	ParentGuid = @ParentGuid,
	HideAfterLogin = @HideAfterLogin,
	CompiledMeta = @CompiledMeta,
	CompiledMetaUtc = @CompiledMetaUtc,
	IncludeInSiteMap = @IncludeInSiteMap,
	IsClickable = @IsClickable,
	ShowHomeCrumb = @ShowHomeCrumb,
	IsPending = @IsPending

WHERE
    PageID = @PageID
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Pages_SelectPendingPageListPage]


/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/

@SiteGuid	uniqueidentifier,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1



CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
[Guid] UniqueIdentifier
)

BEGIN

INSERT INTO #PageIndex ( 
Guid
)

SELECT	[PageGuid]		
FROM	[dbo].[mp_Pages]		
WHERE	SiteGuid = @SiteGuid
AND		IsPending = 1
ORDER BY
		PageName

END

SELECT  p.*,
		wip.Total as WipCount    
FROM    [dbo].mp_Pages p
JOIN	#PageIndex idx
ON		p.[PageGuid] = idx.[Guid]
INNER JOIN			
	(	SELECT	Count(*) as Total,
				pm.PageGuid
		FROM	[dbo].mp_PageModules pm
		INNER JOIN [dbo].mp_ContentWorkflow cw
			ON cw.ModuleGuid = pm.ModuleGuid			
		WHERE	[Status] Not In ('Cancelled','Approved') 
		GROUP BY pm.PageGuid ) as wip
	ON	wip.PageGuid = p.PageGuid
WHERE
		idx.IndexID > @PageLowerBound 
		AND idx.IndexID < @PageUpperBound
ORDER BY p.PageName
GO

SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
Create Procedure [dbo].[mp_Pages_SelectPendingPageCount]

/*
Author:   			Kevin Needham
Created: 			2009-06-19
Last Modified: 		2009-06-19
*/

@SiteGuid uniqueidentifier

As

Select	Count(*)
From	[dbo].mp_Pages
Where	IsPending = 1
And		SiteGuid = @SiteGuid
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE Procedure [dbo].[mp_ContentWorkflow_SelectWorkInProgressByStatus]

/*
Author:   			
Created: 			2009-07-18
Last Modified: 		2009-07-19
*/

@ModuleGuid uniqueidentifier,
@Status nvarchar(20)

As

SELECT	cw.*,
		createdBy.[Name] as CreatedByUserName,
		createdBy.LoginName as CreatedByUserLogin,
		createdBy.Email as CreatedByUserEmail,
		cwah.Notes as Notes,
		cwah.CreatedDateUtc as RecentActionOn,
		recentActionBy.[Name] as RecentActionByUserName,
		recentActionBy.LoginName as RecentActionByUserLogin,
		recentActionBy.Email as RecentActionByUserEmail		
				
FROM
		[dbo].[mp_ContentWorkflow] cw
		
LEFT OUTER JOIN
		[dbo].[mp_Users] createdBy
ON
		createdBy.UserGuid = cw.UserGuid
		
LEFT OUTER JOIN
		[dbo].[mp_ContentWorkflowAuditHistory] cwah
ON	
		cwah.ContentWorkflowGuid = cw.[Guid]
AND		cwah.Active = 1		

LEFT OUTER JOIN
		[dbo].[mp_Users] recentActionBy
ON
		recentActionBy.UserGuid = cwah.UserGuid
		
WHERE
		cw.[ModuleGuid] = @ModuleGuid
AND		 [Status] = @Status
		
ORDER BY
		cw.CreatedDateUtc DESC


GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE PROCEDURE [dbo].[mp_ContentWorkflowAuditHistory_Insert]

/*
Author:   			
Created: 			2009-07-18
Last Modified: 		2009-07-18
*/

@Guid uniqueidentifier,
@ContentWorkflowGuid uniqueidentifier,
@ModuleGuid uniqueidentifier,
@UserGuid uniqueidentifier,
@CreatedDateUtc datetime,
@Status nvarchar(20),
@Notes ntext,
@Active bit


AS


INSERT INTO [dbo].mp_ContentWorkflowAuditHistory 
(
		[Guid],
		[ContentWorkflowGuid],
		[ModuleGuid],
		[UserGuid],
		[CreatedDateUtc],
		[NewStatus],
		[Notes],
		Active
)
VALUES
(
	@Guid,
	@ContentWorkflowGuid,
	@ModuleGuid,
	@UserGuid,
	@CreatedDateUtc,
	@Status,
	@Notes,
	@Active
)
		

GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE PROCEDURE [dbo].[mp_ContentWorkflowAuditHistory_Deactivate]

/*
Author:   			
Created: 			2009-07-18
Last Modified: 		2009-07-18
*/


@ModuleGuid uniqueidentifier


AS


UPDATE	[dbo].mp_ContentWorkflowAuditHistory
SET		Active = 0
WHERE	[ModuleGuid] = @ModuleGuid
		

GO

SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS OFF 
GO


CREATE PROCEDURE [dbo].mp_ContentWorkflow_DeleteByModule

/*
Author:   			
Created: 			2009-7-18
Last Modified: 		2009-7-18
*/

@ModuleGuid uniqueidentifier

AS

DELETE FROM [dbo].[mp_ContentWorkflowAuditHistory]
WHERE
	[ModuleGuid] = @ModuleGuid

DELETE FROM [dbo].[mp_ContentWorkflow]
WHERE
	[ModuleGuid] = @ModuleGuid
	
GO

SET QUOTED_IDENTIFIER ON 
GO
SET ANSI_NULLS OFF 
GO


CREATE PROCEDURE [dbo].mp_ContentWorkflow_DeleteBySite

/*
Author:   			
Created: 			2009-07-18
Last Modified: 		2009-07-18
*/

@SiteGuid uniqueidentifier

AS

DELETE FROM [dbo].[mp_ContentWorkflowAuditHistory]
WHERE
	[ContentWorkflowGuid] IN (SELECT [Guid] 
								FROM [dbo].[mp_ContentWorkflow]
								WHERE
								[SiteGuid] = @SiteGuid)

DELETE FROM [dbo].[mp_ContentWorkflow]
WHERE
	[SiteGuid] = @SiteGuid
	
GO

