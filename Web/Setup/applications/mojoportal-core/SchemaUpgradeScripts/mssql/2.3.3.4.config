SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[mp_UserRoles_SelectNotInRole]

-- Author:   			
-- Last Modified: 		2009-12-26

@SiteID	int,
@RoleID  int,
@PageNumber int,
@PageSize 	int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
UserID Int, [Name] nvarchar(100)
)

BEGIN

INSERT INTO #PageIndex ( UserID, [Name])



SELECT  DISTINCT
    		u.UserID,
    		u.[Name]

FROM		mp_Users  u
    		
    
LEFT OUTER JOIN 		
		mp_UserRoles ur

ON 		u.UserID = ur.UserID
		AND ur.RoleID = @RoleID

WHERE		u.SiteID = @SiteID
    		
			AND ur.RoleID IS NULL

ORDER BY	u.[Name]

END


SELECT
		u.UserID,
    		u.[Name],
    		u.Email,
			u.LoginName
		
FROM
		[dbo].[mp_Users] u

JOIN			#PageIndex t2
ON			
		u.[UserID] = t2.[UserID]
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex



GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[mp_UserRoles_CountNotInRole]

-- Author:   			
-- Created: 			2009-12-26
-- Last Modified: 		2009-12-26

@SiteID	int,
@RoleID  int

AS


SELECT  COUNT(DISTINCT u.UserID)

FROM		mp_Users  u
    		
    
LEFT OUTER JOIN 		
		mp_UserRoles ur

ON 		u.UserID = ur.UserID
		AND ur.RoleID = @RoleID

WHERE		u.SiteID = @SiteID
    		
			AND ur.RoleID IS NULL





GO

