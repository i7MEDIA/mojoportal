ALTER PROCEDURE [dbo].[mp_ForumThreadSubscribers_SelectByThread]

/*
Author:				
Created:			2004-10-13
Last Modified:		2010-10-25

*/

@ForumID int,
@ThreadID		int,
@CurrentPostUserID 	int


AS

SELECT		u.Email,
			COALESCE(fts.ThreadSubscriptionID, -1) AS ThreadSubID,
			COALESCE(fs.SubscriptionID, -1) AS ForumSubID


FROM			[dbo].mp_Users u

LEFT OUTER JOIN [dbo].mp_ForumThreadSubscriptions fts
ON
	fts.UserID = u.UserID
	AND fts.ThreadID = @ThreadID
	AND fts.UnSubscribeDate IS NULL
	
LEFT OUTER JOIN [dbo].mp_ForumSubscriptions fs
ON
	fs.UserID = u.UserID
	AND fs.ForumID = @ForumID
	AND fs.UnSubscribeDate IS NULL


WHERE
			u.UserID <> @CurrentPostUserID
			AND
			(
				(
					fts.ThreadSubscriptionID IS NOT NULL
				)
				OR
				(
					fs.SubscriptionID IS NOT NULL
				)
			)


GO

ALTER PROCEDURE [dbo].[mp_ForumThreads_Insert]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2007

*/

@ForumID			int,
@ThreadSubject		nvarchar(255),
@SortOrder			int,
@IsLocked			bit,
@StartedByUserID		int,
@ThreadDate		datetime


AS
DECLARE @ThreadID int
DECLARE @ForumSequence int
SET @ForumSequence = (SELECT COALESCE(Max(ForumSequence) + 1,1) FROM [dbo].mp_ForumThreads WHERE ForumID = @ForumID)

INSERT INTO		[dbo].mp_ForumThreads
(
			ForumID,
			ThreadSubject,
			SortOrder,
			ForumSequence,
			IsLocked,
			StartedByUserID,
			ThreadDate,
			MostRecentPostUserID,
			MostRecentPostDate

)

VALUES
(
			
			@ForumID,
			@ThreadSubject,
			@SortOrder,
			@ForumSequence,
			@IsLocked,
			@StartedByUserID,
			@ThreadDate,
			@StartedByUserID,
			@ThreadDate


)

SELECT @@IDENTITY  AS ThreadID





GO

