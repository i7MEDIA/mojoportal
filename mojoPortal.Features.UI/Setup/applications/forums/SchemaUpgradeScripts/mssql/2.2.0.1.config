ALTER PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThread]

/*
Author:				
Created:			2005-02-19
Last Modified:		2012-10-26

*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		ft.IsLocked,
		COALESCE(u.[Name],'Guest') AS MostRecentPostUser,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		COALESCE(up.[Email], '') AS AuthorEmail,
		COALESCE(up.TotalPosts, 0) AS PostAuthorTotalPosts,
		COALESCE(up.TotalRevenue, 0) AS UserRevenue,
		COALESCE(up.Trusted, 0) AS Trusted,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.PostID



GO

CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectForSiteMap]

/*
Author:			Joe Audettte
Created:		2012-10-26
Last Modified:	2012-10-26

*/


@SiteID		int

AS
SELECT  	
		ft.ThreadID,
		ft.MostRecentPostDate,
		f.ModuleID,
		m.ViewRoles,
		p.PageID,
		p.AuthorizedRoles

FROM		[dbo].mp_ForumThreads ft


JOIN		[dbo].mp_Forums f
ON		f.ItemID = ft.ForumID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID


JOIN		[dbo].mp_PageModules pm
ON			pm.ModuleID = m.ModuleID

JOIN		[dbo].mp_Pages p
ON		p.PageID = pm.PageID

WHERE	p.SiteID = @SiteID

ORDER BY ft.ThreadID DESC
		


GO

ALTER PROCEDURE [dbo].[mp_ForumPosts_SelectForRss]

/*
Author:				Joseph Hill
Created:			2005-01-11
Last Modified:		2012-10-27 

A list of all forums posts (and associated data) filtered by the specified criteria

MaximumDays is the maximum age in days of the posts to return

*/
@SiteID				int,
@PageID				int,
@ModuleID			int,
@ItemID				int,
@ThreadID			int,
@MaximumDays			int


AS
SELECT		fp.*,
		ft.ThreadSubject,
		ft.ForumID,
		m.ModuleID,
		m.ViewRoles,
		p.PageID,
		p.AuthorizedRoles,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		up.TotalPosts AS PostAuthorTotalPosts,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts fp

JOIN		[dbo].mp_ForumThreads ft
ON		fp.ThreadID = ft.ThreadID

JOIN		[dbo].mp_Forums f
ON		ft.ForumID = f.ItemID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID

JOIN	[dbo].mp_PageModules pm
ON	    m.ModuleID = pm.ModuleID

JOIN		[dbo].mp_Pages p
ON		pm.PageID = p.PageID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = fp.UserID

WHERE	p.SiteID = @SiteID
AND	(@PageID = -1 OR p.PageID = @PageID)
AND	(@ModuleID = -1 OR m.ModuleID = @ModuleID)
AND	(@ItemID = -1 OR f.ItemID = @ItemID)
AND	(@ThreadID = -1 OR ft.ThreadID = @ThreadID)
AND	(@MaximumDays = -1 OR datediff(dd, getdate(), fp.PostDate) <= @MaximumDays)

ORDER BY	fp.PostDate DESC


GO

