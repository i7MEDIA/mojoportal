SET QUOTED_IDENTIFIER ON

GO

CREATE TABLE [dbo].[mp_Forums](
	[ItemID] [int] IDENTITY(1,1) NOT NULL,
	[ModuleID] [int] NOT NULL,
	[CreatedDate] [datetime] NOT NULL,
	[CreatedBy] [int] NOT NULL,
	[Title] [nvarchar](100) NOT NULL,
	[Description] [nvarchar](max) NULL,
	[IsModerated] [bit] NOT NULL,
	[IsActive] [bit] NOT NULL,
	[SortOrder] [int] NOT NULL,
	[ThreadCount] [int] NOT NULL,
	[PostCount] [int] NOT NULL,
	[MostRecentPostDate] [datetime] NULL,
	[MostRecentPostUserID] [int] NOT NULL,
	[PostsPerPage] [int] NOT NULL,
	[ThreadsPerPage] [int] NOT NULL,
	[AllowAnonymousPosts] [bit] NOT NULL,
 CONSTRAINT [PK_mp_ForumBoards] PRIMARY KEY CLUSTERED 
(
	[ItemID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF)
)

GO

SET QUOTED_IDENTIFIER ON

GO

CREATE TABLE [dbo].[mp_ForumSubscriptions](
	[SubscriptionID] [int] IDENTITY(1,1) NOT NULL,
	[ForumID] [int] NOT NULL,
	[UserID] [int] NOT NULL,
	[SubscribeDate] [datetime] NOT NULL,
	[UnSubscribeDate] [datetime] NULL,
 CONSTRAINT [PK_mp_ForumSubscriptions] PRIMARY KEY CLUSTERED 
(
	[SubscriptionID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF)
)

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectPageByUser]

-- Author:   			
-- Created: 			2008-03-18
-- Last Modified: 		2010-07-01

@UserID		int,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
ThreadID Int
)

BEGIN

INSERT INTO #PageIndex ( 
ThreadID
)

SELECT
		[ThreadID]
		
FROM
		[dbo].[mp_ForumThreads]
		
WHERE
	ThreadID IN (Select DISTINCT ThreadID FROM [dbo].mp_ForumPosts WHERE [dbo].mp_ForumPosts.UserID = @UserID)

ORDER BY
	MostRecentPostDate DESC

END

DECLARE @TotalRows int
DECLARE @TotalPages int
DECLARE @Remainder int

SET @TotalRows = (SELECT Count(*) FROM #PageIndex)
SET @TotalPages = @TotalRows / @PageSize
SET @Remainder = @TotalRows % @PageSize
IF @Remainder > 0 
SET @TotalPages = @TotalPages + 1

SELECT
		t.*,
		f.Title As Forum,
		f.ModuleID,
		(SELECT TOP 1 PageID FROM [dbo].mp_PageModules WHERE [dbo].mp_PageModules.ModuleID = f.ModuleID AND (PublishEndDate IS NULL OR PublishEndDate > getutcdate()) ) As PageID,
		COALESCE(u.Name, 'Guest') As MostRecentPostUser,
		s.Name As StartedBy,
		@TotalPages AS TotalPages
		
FROM
		[dbo].[mp_ForumThreads] t

JOIN			#PageIndex t2
ON			
		t.[ThreadID] = t2.[ThreadID]

JOIN	[dbo].mp_Forums f
ON t.ForumID = f.ItemID

LEFT OUTER JOIN	[dbo].mp_Users u
ON t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users s
ON t.StartedByUserID = s.UserID
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex

GO

SET QUOTED_IDENTIFIER ON

GO

CREATE TABLE [dbo].[mp_ForumThreads](
	[ThreadID] [int] IDENTITY(1,1) NOT NULL,
	[ForumID] [int] NOT NULL,
	[ThreadSubject] [nvarchar](255) NOT NULL,
	[ThreadDate] [datetime] NOT NULL,
	[TotalViews] [int] NOT NULL,
	[TotalReplies] [int] NOT NULL,
	[SortOrder] [int] NOT NULL,
	[IsLocked] [bit] NOT NULL,
	[ForumSequence] [int] NOT NULL,
	[MostRecentPostDate] [datetime] NULL,
	[MostRecentPostUserID] [int] NULL,
	[StartedByUserID] [int] NOT NULL,
 CONSTRAINT [PK_mp_ForumThreads] PRIMARY KEY CLUSTERED 
(
	[ThreadID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF)
)

GO

SET QUOTED_IDENTIFIER ON

GO
--~ PAD_INDEX removed.  It is not supported in this version of SQL Server
CREATE TABLE [dbo].[mp_ForumPosts](
	[PostID] [int] IDENTITY(1,1) NOT NULL,
	[ThreadID] [int] NOT NULL,
	[ThreadSequence] [int] NOT NULL,
	[Subject] [nvarchar](255) NOT NULL,
	[PostDate] [datetime] NOT NULL,
	[Approved] [bit] NOT NULL,
	[UserID] [int] NOT NULL,
	[SortOrder] [int] NOT NULL,
	[Post] [nvarchar](max) NULL,
 CONSTRAINT [PK_mp_ForumPosts] PRIMARY KEY CLUSTERED 
(
	[PostID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF)
)

GO

CREATE NONCLUSTERED INDEX [idxforumpostuid] ON [dbo].[mp_ForumPosts] 
(
	[UserID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF, DROP_EXISTING = OFF, ONLINE = OFF)

GO

SET QUOTED_IDENTIFIER ON

GO

CREATE TABLE [dbo].[mp_ForumThreadSubscriptions](
	[ThreadSubscriptionID] [int] IDENTITY(1,1) NOT NULL,
	[ThreadID] [int] NOT NULL,
	[UserID] [int] NOT NULL,
	[SubscribeDate] [datetime] NOT NULL,
	[UnSubscribeDate] [datetime] NULL,
 CONSTRAINT [PK_mp_ForumThreadSubscriptions] PRIMARY KEY CLUSTERED 
(
	[ThreadSubscriptionID] ASC
)WITH (STATISTICS_NORECOMPUTE  = OFF, IGNORE_DUP_KEY = OFF)
)

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_UpdateUserStats]

/*
Author:			
Created:		2009-04-04
Last Modified:	2009-04-04

*/

@UserID			int

AS

UPDATE		[dbo].mp_Users

SET			TotalPosts = (SELECT COUNT(*) 
							FROM	[dbo].mp_ForumPosts
							WHERE [dbo].mp_ForumPosts.UserID = [dbo].mp_Users.UserID)

WHERE		(UserID = @UserID OR @UserID = -1)

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectForRss]

/*
Author:				Joseph Hill
Created:			2005-01-11
Last Modified:		2010-07-01 

A list of all forums posts (and associated data) filtered by the specified criteria

MaximumDays is the maximum age in days of the posts to return

*/
@SiteID				int,
@PageID				int,
@ModuleID			int,
@ItemID				int,
@ThreadID			int,
@MaximumDays			int


AS
SELECT		fp.*,
		ft.ThreadSubject,
		ft.ForumID,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		up.TotalPosts AS PostAuthorTotalPosts,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts fp

JOIN		[dbo].mp_ForumThreads ft
ON		fp.ThreadID = ft.ThreadID

JOIN		[dbo].mp_Forums f
ON		ft.ForumID = f.ItemID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID

JOIN	[dbo].mp_PageModules pm
ON	    m.ModuleID = pm.ModuleID

JOIN		[dbo].mp_Pages p
ON		pm.PageID = p.PageID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = fp.UserID

WHERE	p.SiteID = @SiteID
AND	(@PageID = -1 OR p.PageID = @PageID)
AND	(@ModuleID = -1 OR m.ModuleID = @ModuleID)
AND	(@ItemID = -1 OR f.ItemID = @ItemID)
AND	(@ThreadID = -1 OR ft.ThreadID = @ThreadID)
AND	(@MaximumDays = -1 OR datediff(dd, getdate(), fp.PostDate) <= @MaximumDays)

ORDER BY	fp.PostDate DESC

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_SelectOne]

/*
Author:				
Created:			2004-09-12
Last Modified:		2010-07-01

*/

@ItemID			int

AS

SELECT		f.*,
			u.[Name] AS CreatedByUser,
			up.[Name] AS MostRecentPostUser

FROM			[dbo].mp_Forums f

LEFT OUTER JOIN	[dbo].mp_Users u
ON			f.CreatedBy = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users up
ON			f.MostRecentPostUserID = up.UserID

WHERE		f.ItemID = @ItemID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThread]

/*
Author:				
Created:			2005-02-19
Last Modified:		2010-07-01

*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		COALESCE(u.[Name],'Guest') AS MostRecentPostUser,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		COALESCE(up.TotalPosts, 0) AS PostAuthorTotalPosts,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.PostID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectByThread]

/*
Author:				
Created:			2004-09-14
Last Modified:		2010-03-26



*/

@ThreadID			int,
@PageNumber			int

AS

DECLARE @PostsPerPage	int

DECLARE @CurrentPageMaxThreadSequence	int

DECLARE @BeginSequence int
DECLARE @EndSequence int



SELECT	@PostsPerPage = f.PostsPerPage


FROM		[dbo].mp_ForumThreads ft

JOIN		[dbo].mp_Forums f
ON		ft.ForumID = f.ItemID

WHERE	ft.ThreadID = @ThreadID

SET @CurrentPageMaxThreadSequence = (@PostsPerPage * @PageNumber) 
IF @CurrentPageMaxThreadSequence > @PostsPerPage
	BEGIN
		SET @BeginSequence = @CurrentPageMaxThreadSequence  - @PostsPerPage + 1
	END
ELSE
	BEGIN
		SET @BeginSequence = 1
	END

SET @EndSequence = @BeginSequence + @PostsPerPage  -1



SELECT	p.*,
		ft.ForumID,
		COALESCE(u.[Name],'Guest') AS MostRecentPostUser,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		COALESCE(up.[Email], '') AS AuthorEmail,
		COALESCE(up.TotalPosts, 0) AS PostAuthorTotalPosts,
		COALESCE(up.TotalRevenue, 0) AS UserRevenue,
		COALESCE(up.Trusted, 0) AS Trusted,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		AND p.ThreadSequence >= @BeginSequence
		AND  p.ThreadSequence <= @EndSequence

ORDER BY	p.SortOrder, p.PostID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThreadRevereSorted]

/*
Author:				
Created:			2007-07-17
Last Modified:		2010-07-01

*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		COALESCE(u.[Name],'Guest') AS MostRecentPostUser,
		COALESCE(s.[Name],'Guest') AS StartedBy,
		COALESCE(up.[Name], 'Guest') AS PostAuthor,
		COALESCE(up.[Email], '') AS AuthorEmail,
		COALESCE(up.TotalPosts, 0) AS PostAuthorTotalPosts,
		COALESCE(up.TotalRevenue, 0) AS UserRevenue,
		COALESCE(up.Trusted, 0) AS Trusted,
		up.AvatarUrl AS PostAuthorAvatar,
		up.WebSiteURL AS PostAuthorWebSiteUrl,
		up.[Signature] AS PostAuthorSignature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.ThreadSequence DESC

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_Select]


/*
Author:			
Created:		2004-09-12
Last Modified:	2009-12-12

*/

@ModuleID			int,
@UserID				int

AS


SELECT		f.*,
			u.[Name] AS MostRecentPostUser,
			CASE WHEN s.[SubscribeDate] IS NOT NULL and s.[UnSubscribeDate] IS NULL THEN 1
					Else 0
					End AS Subscribed,
			(SELECT COUNT(*) 
				FROM [dbo].mp_ForumSubscriptions fs 
				WHERE fs.ForumID = f.ItemID AND fs.UnSubscribeDate IS NULL) AS SubscriberCount

FROM			[dbo].mp_Forums f

LEFT OUTER JOIN	[dbo].mp_Users u
ON			f.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN [dbo].mp_ForumSubscriptions s
on			f.ItemID = s.ForumID and s.UserID = @UserID

WHERE		f.ModuleID	= @ModuleID
			AND f.IsActive = 1

ORDER BY		f.SortOrder, f.ItemID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectOne]

/*
Author:			
Created:		2004-09-19
Last Modified:	2010-07-01

*/

@ThreadID		int


AS

SELECT		t.*,
			COALESCE(u.[Name], 'Guest') AS MostRecentPostUser,
			COALESCE(s.[Name], 'Guest') AS StartedBy,
			f.PostsPerPage


FROM			[dbo].mp_ForumThreads t

LEFT OUTER JOIN	[dbo].mp_Users u
ON			t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users s
ON			t.StartedByUserID = s.UserID

JOIN			[dbo].mp_Forums f
ON			f.ItemID = t.ForumID

WHERE		t.ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscribers_SelectByThread]

/*
Author:				
Created:			2004-10-13
Last Modified:		2010-03-20

*/

@ForumID int,
@ThreadID		int,
@CurrentPostUserID 	int


AS

SELECT		u.Email


FROM			[dbo].mp_Users u

WHERE
			u.UserID <> @CurrentPostUserID
			AND
			(
				(
					u.UserID IN (SELECT UserID FROM [dbo].mp_ForumThreadSubscriptions
							WHERE ThreadID = @ThreadID
								AND UnSubscribeDate IS NULL)
				)
				OR
				(
					u.UserID IN (SELECT UserID FROM [dbo].mp_ForumSubscriptions
							WHERE ForumID = @ForumID
								AND UnSubscribeDate IS NULL)
				)
			)

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByPage]

/*
Author:			Joe Audettte
Created:		2005-07-02
Last Modified:	2009-06-24

*/


@SiteID		int,
@PageID		int

AS
SELECT  	fp.*,
		f.ModuleID,
		f.ItemID,
		m.ModuleTitle,
		m.ViewRoles,
		md.FeatureName

FROM		[dbo].mp_ForumPosts fp

JOIN		[dbo].mp_ForumThreads ft
ON		fp.ThreadID = ft.ThreadID

JOIN		[dbo].mp_Forums f
ON		f.ItemID = ft.ForumID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID

JOIN		[dbo].mp_ModuleDefinitions md
ON		m.ModuleDefID = md.ModuleDefID

JOIN		[dbo].mp_PageModules pm
ON			pm.ModuleID = m.ModuleID

JOIN		[dbo].mp_Pages p
ON		p.PageID = pm.PageID

WHERE	p.SiteID = @SiteID
		AND pm.PageID = @PageID
		AND fp.Approved = 1

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_Delete]

/*
Author:				
Created:			6/27/2006
Last Modified:		12/11/2006

*/

@ItemID			int

AS

DELETE FROM [dbo].mp_ForumPosts

WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads
					WHERE ForumID = @ItemID)

DELETE FROM [dbo].mp_ForumThreadSubscriptions

WHERE	ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads
					WHERE ForumID = @ItemID)

DELETE
FROM [dbo].mp_ForumThreads
WHERE ForumID = @ItemID

DELETE
FROM [dbo].mp_ForumSubscriptions
WHERE ForumID = @ItemID


DELETE
FROM			[dbo].mp_Forums 

WHERE		ItemID = @ItemID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_RecalculatePostStats]

/*
Author:			
Created:		2005-09-11
Last Modified:	2009-02-11

based ont he pgsql version by Dean Brettle

*/

@ForumID		int


AS

DECLARE @RowsAffected		int
DECLARE @MostRecentPostDate	datetime
DECLARE @MostRecentPostUserID	int
DECLARE @PostCount			int

SET @RowsAffected = 0

SELECT TOP 1	@MostRecentPostDate = MostRecentPostDate,
		@MostRecentPostUserID = MostRecentPostUserID

FROM		[dbo].mp_ForumThreads

WHERE	ForumID = @ForumID

ORDER BY	MostRecentPostDate DESC

SET @PostCount = COALESCE(
				(	SELECT 	SUM(TotalReplies) + COUNT(*)
					FROM		[dbo].mp_ForumThreads
					WHERE	ForumID = @ForumID

				),
				0
				)

UPDATE 	[dbo].mp_Forums
SET		MostRecentPostDate = @MostRecentPostDate,
		MostRecentPostUserID = COALESCE(@MostRecentPostUserID,0),
		PostCount = @PostCount

WHERE	ItemID = @ForumID

SET @RowsAffected = @@ROWCOUNT


SELECT @RowsAffected

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByForumDesc_v2]

/*
Author:			
Created:		2004-09-25
Last Modified:	2010-04-13

*/


@ForumID			int,
@PageNumber			int

AS

DECLARE @ThreadsPerPage	int
DECLARE @TotalThreads	int

SELECT	@ThreadsPerPage = ThreadsPerPage,
		@TotalThreads = ThreadCount
FROM		mp_Forums
WHERE	ItemID = @ForumID


DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@ThreadsPerPage * @PageNumber) - @ThreadsPerPage
SET @PageUpperBound = @PageLowerBound + @ThreadsPerPage + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
	ThreadID int
	
)

INSERT INTO #PageIndex (ThreadID)


SELECT	t.ThreadID
FROM		[dbo].mp_ForumThreads t
WHERE	t.ForumID = @ForumID	
ORDER BY t.SortOrder,	t.MostRecentPostDate DESC


SELECT	t.*,
		u.[Name] AS MostRecentPostUser,
		s.[Name] AS StartedBy


FROM		[dbo].mp_ForumThreads t

JOIN		#PageIndex p
ON		p.ThreadID = t.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		t.StartedByUserID = s.UserID

WHERE	t.ForumID = @ForumID

		

AND p.IndexID > @PageLowerBound 
		AND p.IndexID < @PageUpperBound

ORDER BY	p.IndexID 

DROP TABLE #PageIndex

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_DecrementThreadCount]

/*
Author:			
Created:		11/28/2004
Last Modified:		11/28/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount - 1

WHERE		ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementPostCount]

/*
Author:				
Created:			11/6/2004
Last Modified:			1/14/2007

*/

@ForumID			int,
@MostRecentPostUserID	int,
@MostRecentPostDate datetime

AS
UPDATE 	[dbo].mp_Forums

SET 		MostRecentPostDate = @MostRecentPostDate,
		MostRecentPostUserID = @MostRecentPostUserID,
 		PostCount = PostCount + 1

WHERE 	ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementPostCountOnly]

/*
Author:				
Created:			9/10/2005
Last Modified:			9/10/2005

*/

@ForumID			int


AS


UPDATE 	[dbo].mp_Forums

SET 		
 		PostCount = PostCount + 1

WHERE 	ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_UpdateThreadStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount + 1

WHERE		ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_UpdatePostStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ForumID			int,
@MostRecentPostUserID	int

AS

UPDATE	[dbo].mp_Forums

SET		MostRecentPostDate = GetDate(),
		MostRecentPostUserID = @MostRecentPostUserID,
		PostCount = PostCount + 1

WHERE	ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_DeleteBySite]

/*
Author:				
Created:			2008-11-12
Last Modified:		2008-11-12

*/

@SiteID			int

AS

DELETE FROM [dbo].mp_ForumPosts
WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID IN  (SELECT ModuleID FROM [dbo].mp_Modules WHERE SiteID = @SiteID)) )

DELETE FROM [dbo].mp_ForumThreadSubscriptions
WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID IN  (SELECT ModuleID FROM [dbo].mp_Modules WHERE SiteID = @SiteID)) )

DELETE FROM [dbo].mp_ForumThreads
WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID IN  
(SELECT ModuleID FROM [dbo].mp_Modules WHERE SiteID = @SiteID)) 

DELETE FROM [dbo].mp_ForumSubscriptions
WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID IN  
(SELECT ModuleID FROM [dbo].mp_Modules WHERE SiteID = @SiteID)) 

DELETE FROM [dbo].mp_Forums
WHERE ModuleID IN (SELECT ModuleID FROM [dbo].mp_Modules WHERE SiteID = @SiteID)

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_DeleteByModule]

/*
Author:				
Created:			2009-06-22
Last Modified:		2009-06-22

*/

@ModuleID			int

AS

DELETE FROM [dbo].mp_ForumPosts
WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID = @ModuleID ) )

DELETE FROM [dbo].mp_ForumThreadSubscriptions
WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID = @ModuleID) )

DELETE FROM [dbo].mp_ForumThreads
WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID = @ModuleID )

DELETE FROM [dbo].mp_ForumSubscriptions
WHERE ForumID IN
(SELECT ItemID FROM [dbo].mp_Forums WHERE ModuleID = @ModuleID) 

DELETE FROM mp_Forums
WHERE ModuleID = @ModuleID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_Update]

/*
Author:			
Created:		2004-09-12
Last Modified:	2010-07-01

*/

@ItemID			int,
@Title          			nvarchar(100),
@Description    			nvarchar(max),
@IsModerated			bit,
@IsActive			bit,
@SortOrder			int,
@PostsPerPage			int,
@ThreadsPerPage		int,
@AllowAnonymousPosts		bit



AS


UPDATE		[dbo].mp_Forums

SET			Title = @Title,
			[Description] = @Description,
			IsModerated = @IsModerated,
			IsActive = @IsActive,
			SortOrder = @SortOrder,
			PostsPerPage = @PostsPerPage,
			ThreadsPerPage = @ThreadsPerPage,
			AllowAnonymousPosts = @AllowAnonymousPosts



WHERE		ItemID = @ItemID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_DecrementPostCount]

/*
Author:				
Created:			11/6/2004
Last Modified:			11/6/2004

*/

@ForumID		int

AS


UPDATE [dbo].mp_Forums

SET PostCount = PostCount - 1

WHERE ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_Forums_Insert]

/*
Author:				
Created:			2004-09-12
Last Modified:		2010-07-01

*/

@ModuleID			int,
@UserID			int,
@Title          			nvarchar(100),
@Description    			nvarchar(max),
@IsModerated			bit,
@IsActive			bit,
@SortOrder			int,
@PostsPerPage			int,
@ThreadsPerPage		int,
@AllowAnonymousPosts		bit



AS

INSERT INTO			[dbo].mp_Forums
(
				ModuleID,
				CreatedBy,
				Title,
				[Description],
				IsModerated,
				IsActive,
				SortOrder,
				PostsPerPage,
				ThreadsPerPage,
				AllowAnonymousPosts

)

VALUES
(
				@ModuleID,
				@UserID,
				@Title,
				@Description,
				@IsModerated,
				@IsActive,
				@SortOrder,
				@PostsPerPage,
				@ThreadsPerPage,
				@AllowAnonymousPosts

)

SELECT @@IDENTITY As ItemID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementThreadCount]

/*
Author:			
Created:		11/28/2004
Last Modified:		11/28/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount + 1

WHERE		ItemID = @ForumID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Insert]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2007

*/

@ForumID			int,
@ThreadSubject		nvarchar(255),
@SortOrder			int,
@IsLocked			bit,
@StartedByUserID		int,
@ThreadDate		datetime


AS
DECLARE @ThreadID int
DECLARE @ForumSequence int
SET @ForumSequence = (SELECT COALESCE(Max(ForumSequence) + 1,1) FROM [dbo].mp_ForumThreads WHERE ForumID = @ForumID)

INSERT INTO		[dbo].mp_ForumThreads
(
			ForumID,
			ThreadSubject,
			SortOrder,
			ForumSequence,
			IsLocked,
			StartedByUserID,
			ThreadDate,
			MostRecentPostUserID,
			MostRecentPostDate

)

VALUES
(
			
			@ForumID,
			@ThreadSubject,
			@SortOrder,
			@ForumSequence,
			@IsLocked,
			@StartedByUserID,
			@ThreadDate,
			@StartedByUserID,
			@ThreadDate


)

SELECT @ThreadID = @@IDENTITY 


INSERT INTO [dbo].mp_ForumThreadSubscriptions (ThreadID, UserID)
	SELECT @ThreadID, UserID FROM [dbo].mp_ForumSubscriptions fs 
		WHERE fs.ForumID = @ForumID AND fs.SubscribeDate IS NOT NULL 
				AND fs.UnSubscribeDate IS NULL

SELECT @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Insert]

/*
Author:			Dean Brettle
Created:		9/11/2005
Last Modified:		9/11/2005

*/

@ForumID		int,
@UserID		int


AS

IF EXISTS (SELECT UserID FROM mp_ForumSubscriptions WHERE ForumID = @ForumID AND UserID = @UserID)
BEGIN
	UPDATE 	[dbo].mp_ForumSubscriptions

	SET		SubscribeDate = GetDate(),
			UnSubscribeDate = Null
	

	WHERE 	ForumID = @ForumID AND UserID = @UserID

END

ELSE

BEGIN

	INSERT INTO	[dbo].mp_ForumSubscriptions
	(
			ForumID,
			UserID
	)
	VALUES
	(
			@ForumID,
			@UserID
	)

END

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Unsubscribe]

/*
Author:				Dean Brettle
Created:			9/11/2005
Last Modified:			9/11/2005

*/

@ForumID		int,
@UserID		int

AS

UPDATE		[dbo].mp_ForumSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		ForumID = @ForumID
			AND UserID = @UserID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Exists]

/*
Author:			
Created:		2007/05/24
Last Modified:	2007/05/24

*/
    
@ForumID  	int,
@UserID		int

AS
IF EXISTS (	SELECT 	SubscriptionID
		FROM		[dbo].mp_ForumSubscriptions
		WHERE	ForumID = @ForumID
				AND UserID = @UserID
				AND UnSubscribeDate is null )
SELECT 1
ELSE
SELECT 0

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_UnsubscribeAll]

/*
Author:				
Created:			2009-05-01
Last Modified:		2009-05-01

*/


@UserID		int

AS

UPDATE		[dbo].mp_ForumSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		UserID = @UserID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_GetCount]

/*
Author:   			
Created: 			2009-12-12
Last Modified: 		2009-12-12
*/

@ForumID int

AS

SELECT COUNT(*) 
FROM [dbo].[mp_ForumSubscriptions]
WHERE ForumID = @ForumID
AND UnSubscribeDate IS NULL

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Delete]

/*
Author:   			
Created: 			2009-12-12
Last Modified: 		2009-12-12
*/

@SubscriptionID int

AS

DELETE FROM [dbo].[mp_ForumSubscriptions]
WHERE
	[SubscriptionID] = @SubscriptionID

GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_CountByUser]    Script Date: 07/13/2010 13:09:04 ******/


GO
SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_CountByUser]

/*
Author:				
Created:			2008-03-18
Last Modified:		2008-03-18

*/


@UserID		int

AS

SELECT	COUNT(*)

FROM		[dbo].mp_ForumThreads

WHERE ThreadID IN (Select DISTINCT ThreadID FROM [dbo].mp_ForumPosts WHERE [dbo].mp_ForumPosts.UserID = @UserID)

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_DecrementReplyCount]

/*
Author:			
Created:		2/19/2005
Last Modified:		2/19/2005

*/

@ThreadID			int


AS

DECLARE @MostRecentPostUserID int
DECLARE @MostRecentPostDate datetime
 
SELECT TOP 1  @MostRecentPostUserID = UserID,
		@MostRecentPostDate = PostDate

FROM [dbo].mp_ForumPosts 
WHERE ThreadID = @ThreadID 
ORDER BY PostID DESC


UPDATE		[dbo].mp_ForumThreads

SET			MostRecentPostUserID = @MostRecentPostUserID,
			TotalReplies = TotalReplies - 1,
			MostRecentPostDate = @MostRecentPostDate


WHERE		ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Delete]

/*
Author:			
Created:		11/28/2004
Last Modified:	8/14/2006

*/

@ThreadID			int

AS

DELETE FROM [dbo].mp_ForumThreadSubscriptions

WHERE	ThreadID = @ThreadID

DELETE FROM [dbo].mp_ForumPosts

WHERE	ThreadID = @ThreadID

DELETE FROM 		[dbo].mp_ForumThreads


WHERE		ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Delete]

/*
Author:			
Created:		11/6/2004
Last Modified:		11/6/2004

*/

@PostID		int


AS

DELETE FROM [dbo].mp_ForumPosts

WHERE PostID = @PostID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_UpdateSequence]

/*
Author:			
Created:		2007-06-07
Last Modified:	2007-06-07

*/

@PostID			int,
@ThreadSequence			int


AS

UPDATE		[dbo].mp_ForumPosts

SET			ThreadSequence = @ThreadSequence


WHERE		PostID = @PostID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Update]

/*
Author:			
Created:		2004-09-19
Last Modified:	2010-07-01

*/

@PostID			int,
@Subject			nvarchar(255),
@Post				nvarchar(max),
@SortOrder			int,
@Approved			bit

AS

UPDATE		[dbo].mp_ForumPosts

SET			Subject = @Subject,
			Post = @Post,
			SortOrder = @SortOrder,
			Approved = @Approved


WHERE		PostID = @PostID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Insert]

/*
Author:			
Created:		2004-09-19
Last Modified:	2010-07-01

*/


@ThreadID			int,
@Subject			nvarchar(255),
@Post				nvarchar(max),
@Approved			bit,
@UserID			int,
@PostDate		datetime



AS
DECLARE @ThreadSequence int
SET @ThreadSequence = (SELECT COALESCE(Max(ThreadSequence) + 1,1) FROM [dbo].mp_ForumPosts WHERE ThreadID = @ThreadID)



INSERT INTO		[dbo].mp_ForumPosts
(
			ThreadID,
			Subject,
			Post,
			Approved,
			UserID,
			ThreadSequence,
			PostDate
)

VALUES
(
			@ThreadID,
			@Subject,
			@Post,
			@Approved,
			@UserID,
			@ThreadSequence,
			@PostDate

)

SELECT  @@IDENTITY As PostID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectOne]

/*
Author:				
Created:			10/17/2004
Last Modified:			10/17/2004

*/

@PostID		int

AS


SELECT	fp.*

FROM		[dbo].mp_ForumPosts fp

WHERE	fp.PostID = @PostID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectSortedThread]

/*
Author:				
Created:			2007-06-08
Last Modified:		2007-06-08


*/

@ThreadID			int

AS

SELECT	*
		
FROM		[dbo].mp_ForumPosts


WHERE	ThreadID = @ThreadID
		

ORDER BY	ThreadSequence

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_CountByThread]

/*
Author:				
Created:			11/28/2004
Last Modified:			11/28/2004

*/


@ThreadID		int



AS


SELECT	COUNT(*)

FROM		[dbo].mp_ForumPosts

WHERE	ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_Exists]

/*
Author:			
Created:		2007/05/24
Last Modified:	2007/05/24

*/
    
@ThreadID  	int,
@UserID		int

AS
IF EXISTS (	SELECT 	ThreadSubscriptionID
		FROM		[dbo].mp_ForumThreadSubscriptions
		WHERE	ThreadID = @ThreadID
				AND UserID = @UserID
				AND UnSubscribeDate is null )
SELECT 1
ELSE
SELECT 0

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_UnsubscribeThread]

/*
Author:				
Created:			10/14/2004
Last Modified:			10/14/2004

*/

@ThreadID		int,
@UserID		int

AS

UPDATE		[dbo].mp_ForumThreadSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		ThreadID = @ThreadID
			AND UserID = @UserID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_UnsubscribeAllThreads]

/*
Author:				
Created:			10/14/2004
Last Modified:			10/14/2004

*/

@UserID		int

AS

UPDATE		[dbo].mp_ForumThreadSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		
			UserID = @UserID

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_Insert]

/*
Author:			
Created:		2004-10-14
Last Modified:	2010-07-01

*/

@ThreadID		int,
@UserID		int


AS

IF EXISTS (SELECT UserID FROM [dbo].mp_ForumThreadSubscriptions WHERE ThreadID = @ThreadID AND UserID = @UserID)
BEGIN
	UPDATE 	[dbo].mp_ForumThreadSubscriptions

	SET		SubscribeDate = GetUtcDate(),
			UnSubscribeDate = Null
	

	WHERE 	ThreadID = @ThreadID AND UserID = @UserID

END

ELSE

BEGIN

	INSERT INTO	[dbo].mp_ForumThreadSubscriptions
	(
			ThreadID,
			UserID
	)
	VALUES
	(
			@ThreadID,
			@UserID
	)

END

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_UpdateViewStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ThreadID			int



AS


UPDATE		[dbo].mp_ForumThreads

SET		
			TotalViews = TotalViews + 1


WHERE		ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_IncrementReplyCount]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2006

*/

@ThreadID			int,
@MostRecentPostUserID	int,
@MostRecentPostDate datetime



AS
UPDATE		[dbo].mp_ForumThreads

SET			MostRecentPostUserID = @MostRecentPostUserID,
			TotalReplies = TotalReplies + 1,
			MostRecentPostDate = @MostRecentPostDate


WHERE		ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Update]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ThreadID			int,
@ForumID			int,
@ThreadSubject		nvarchar(255),
@SortOrder			int,
@IsLocked			bit


AS


UPDATE		[dbo].mp_ForumThreads

SET			ForumID = @ForumID,
			ThreadSubject = @ThreadSubject,
			SortOrder = @SortOrder,
			IsLocked = @IsLocked


WHERE		ThreadID = @ThreadID

GO

SET QUOTED_IDENTIFIER OFF

GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_UpdateForumSequence]

/*
Author:			
Created:		2007-06-07
Last Modified:	2007-06-07

*/

@ThreadID			int,
@ForumSequence			int


AS


UPDATE		[dbo].mp_ForumThreads

SET			ForumSequence = @ForumSequence


WHERE		ThreadID = @ThreadID

GO

ALTER TABLE [dbo].[mp_ForumPosts] ADD  CONSTRAINT [DF_mp_ForumPosts_ThreadSequence]  DEFAULT ((1)) FOR [ThreadSequence]

GO

ALTER TABLE [dbo].[mp_ForumPosts] ADD  CONSTRAINT [DF_mp_ForumPosts_PostDate]  DEFAULT (getdate()) FOR [PostDate]

GO

ALTER TABLE [dbo].[mp_ForumPosts] ADD  CONSTRAINT [DF_mp_ForumPosts_Approved]  DEFAULT ((0)) FOR [Approved]

GO

ALTER TABLE [dbo].[mp_ForumPosts] ADD  CONSTRAINT [DF_mp_ForumPosts_UserID]  DEFAULT ((-1)) FOR [UserID]

GO

ALTER TABLE [dbo].[mp_ForumPosts] ADD  CONSTRAINT [DF_mp_ForumPosts_SortOrder]  DEFAULT ((100)) FOR [SortOrder]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_DateCreated]  DEFAULT (getdate()) FOR [CreatedDate]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_Moderated]  DEFAULT ((0)) FOR [IsModerated]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_Active]  DEFAULT ((1)) FOR [IsActive]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_SortOrder]  DEFAULT ((100)) FOR [SortOrder]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_TotalPosts]  DEFAULT ((0)) FOR [ThreadCount]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_TotalThreads]  DEFAULT ((0)) FOR [PostCount]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_ForumBoards_MostRecentPostUserID]  DEFAULT ((-1)) FOR [MostRecentPostUserID]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_Forums_EntriesPerPage]  DEFAULT ((10)) FOR [PostsPerPage]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_Forums_ThreadsPerPage]  DEFAULT ((40)) FOR [ThreadsPerPage]

GO

ALTER TABLE [dbo].[mp_Forums] ADD  CONSTRAINT [DF_mp_Forums_AllowAnonymousPosts]  DEFAULT ((1)) FOR [AllowAnonymousPosts]

GO

ALTER TABLE [dbo].[mp_ForumSubscriptions] ADD  CONSTRAINT [DF_mp_ForumSubscriptions_SubscribeDate]  DEFAULT (getdate()) FOR [SubscribeDate]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_ThreadDate]  DEFAULT (getdate()) FOR [ThreadDate]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_TotalViews]  DEFAULT ((0)) FOR [TotalViews]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_TotalReplies]  DEFAULT ((0)) FOR [TotalReplies]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_SortOrder]  DEFAULT ((1000)) FOR [SortOrder]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_IsLocked]  DEFAULT ((0)) FOR [IsLocked]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_ForumSequence]  DEFAULT ((1)) FOR [ForumSequence]

GO

ALTER TABLE [dbo].[mp_ForumThreads] ADD  CONSTRAINT [DF_mp_ForumThreads_MostRecentPostDate]  DEFAULT (getdate()) FOR [MostRecentPostDate]

GO

ALTER TABLE [dbo].[mp_ForumThreadSubscriptions] ADD  CONSTRAINT [DF_mp_ForumThreadSubscriptions_SubscribeDate]  DEFAULT (getdate()) FOR [SubscribeDate]

GO

ALTER TABLE [dbo].[mp_ForumPosts]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumPosts_mp_ForumThreads] FOREIGN KEY([ThreadID])
REFERENCES [dbo].[mp_ForumThreads] ([ThreadID])

GO
ALTER TABLE [dbo].[mp_ForumPosts] CHECK CONSTRAINT [FK_mp_ForumPosts_mp_ForumThreads]

GO

ALTER TABLE [dbo].[mp_ForumThreads]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumThreads_mp_Forums] FOREIGN KEY([ForumID])
REFERENCES [dbo].[mp_Forums] ([ItemID])

GO
ALTER TABLE [dbo].[mp_ForumThreads] CHECK CONSTRAINT [FK_mp_ForumThreads_mp_Forums]

GO

ALTER TABLE [dbo].[mp_ForumThreadSubscriptions]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumThreadSubscriptions_mp_ForumThreads] FOREIGN KEY([ThreadID])
REFERENCES [dbo].[mp_ForumThreads] ([ThreadID])

GO
ALTER TABLE [dbo].[mp_ForumThreadSubscriptions] CHECK CONSTRAINT [FK_mp_ForumThreadSubscriptions_mp_ForumThreads]

GO

SET QUOTED_IDENTIFIER ON

GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_SelectPage]

-- Author:   			
-- Created: 			2009-12-12
-- Last Modified: 		2009-12-12

@ForumID int,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1


CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
SubscriptionID Int
)

BEGIN

INSERT INTO #PageIndex ( 
SubscriptionID
)

SELECT
		fs.[SubscriptionID]
		
FROM
		[dbo].[mp_ForumSubscriptions] fs
		
LEFT OUTER JOIN
		[dbo].mp_Users u
ON
		u.UserID = fs.UserID
		
WHERE
	fs.ForumID = @ForumID
	AND fs.UnSubscribeDate IS NULL

ORDER BY
	u.[Name]

END


SELECT
		t1.*,
		u.[Name],
		u.LoginName,
		u.Email
		
FROM
		[dbo].[mp_ForumSubscriptions] t1

JOIN			#PageIndex t2
ON			
		t1.[SubscriptionID] = t2.[SubscriptionID]
		
LEFT OUTER JOIN
		[dbo].mp_Users u
ON
		u.UserID = t1.UserID
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex

GO
