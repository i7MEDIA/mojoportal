/****** Object:  Table [dbo].[mp_Forums]    Script Date: 11/10/2008 13:12:27 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_Forums](
	[ItemID] [int] IDENTITY(1,1) NOT NULL,
	[ModuleID] [int] NOT NULL,
	[CreatedDate] [datetime] NOT NULL CONSTRAINT [DF_mp_ForumBoards_DateCreated]  DEFAULT (getdate()),
	[CreatedBy] [int] NOT NULL,
	[Title] [nvarchar](100) NOT NULL,
	[Description] [ntext] NOT NULL,
	[IsModerated] [bit] NOT NULL CONSTRAINT [DF_mp_ForumBoards_Moderated]  DEFAULT ((0)),
	[IsActive] [bit] NOT NULL CONSTRAINT [DF_mp_ForumBoards_Active]  DEFAULT ((1)),
	[SortOrder] [int] NOT NULL CONSTRAINT [DF_mp_ForumBoards_SortOrder]  DEFAULT ((100)),
	[ThreadCount] [int] NOT NULL CONSTRAINT [DF_mp_ForumBoards_TotalPosts]  DEFAULT ((0)),
	[PostCount] [int] NOT NULL CONSTRAINT [DF_mp_ForumBoards_TotalThreads]  DEFAULT ((0)),
	[MostRecentPostDate] [datetime] NULL,
	[MostRecentPostUserID] [int] NOT NULL CONSTRAINT [DF_mp_ForumBoards_MostRecentPostUserID]  DEFAULT ((-1)),
	[PostsPerPage] [int] NOT NULL CONSTRAINT [DF_mp_Forums_EntriesPerPage]  DEFAULT ((10)),
	[ThreadsPerPage] [int] NOT NULL CONSTRAINT [DF_mp_Forums_ThreadsPerPage]  DEFAULT ((40)),
	[AllowAnonymousPosts] [bit] NOT NULL CONSTRAINT [DF_mp_Forums_AllowAnonymousPosts]  DEFAULT ((1)),
 CONSTRAINT [PK_mp_ForumBoards] PRIMARY KEY CLUSTERED 
(
	[ItemID] ASC
) ON [PRIMARY]
) ON [PRIMARY] TEXTIMAGE_ON [PRIMARY]
GO
/****** Object:  Table [dbo].[mp_ForumSubscriptions]    Script Date: 11/10/2008 13:12:29 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ForumSubscriptions](
	[SubscriptionID] [int] IDENTITY(1,1) NOT FOR REPLICATION NOT NULL,
	[ForumID] [int] NOT NULL,
	[UserID] [int] NOT NULL,
	[SubscribeDate] [datetime] NOT NULL CONSTRAINT [DF_mp_ForumSubscriptions_SubscribeDate]  DEFAULT (getdate()),
	[UnSubscribeDate] [datetime] NULL,
 CONSTRAINT [PK_mp_ForumSubscriptions] PRIMARY KEY CLUSTERED 
(
	[SubscriptionID] ASC
) ON [PRIMARY]
) ON [PRIMARY]
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectPageByUser]    Script Date: 11/10/2008 13:12:16 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectPageByUser]

-- Author:   			
-- Created: 			2008-3-18
-- Last Modified: 		2008-3-18

@UserID		int,
@PageNumber 			int,
@PageSize 			int

AS

DECLARE @PageLowerBound int
DECLARE @PageUpperBound int


SET @PageLowerBound = (@PageSize * @PageNumber) - @PageSize
SET @PageUpperBound = @PageLowerBound + @PageSize + 1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
ThreadID Int
)

BEGIN

INSERT INTO #PageIndex ( 
ThreadID
)

SELECT
		[ThreadID]
		
FROM
		[dbo].[mp_ForumThreads]
		
WHERE
	ThreadID IN (Select DISTINCT ThreadID FROM mp_ForumPosts WHERE mp_ForumPosts.UserID = @UserID)

ORDER BY
	MostRecentPostDate DESC

END

DECLARE @TotalRows int
DECLARE @TotalPages int
DECLARE @Remainder int

SET @TotalRows = (SELECT Count(*) FROM #PageIndex)
SET @TotalPages = @TotalRows / @PageSize
SET @Remainder = @TotalRows % @PageSize
IF @Remainder > 0 
SET @TotalPages = @TotalPages + 1

SELECT
		t.*,
		f.Title As Forum,
		f.ModuleID,
		(SELECT TOP 1 PageID FROM mp_PageModules WHERE mp_PageModules.ModuleID = f.ModuleID AND (PublishEndDate IS NULL OR PublishEndDate > getutcdate()) ) As PageID,
		COALESCE(u.Name, 'Guest') As MostRecentPostUser,
		s.Name As StartedBy,
		'TotalPages' = @TotalPages
		
FROM
		[dbo].[mp_ForumThreads] t

JOIN			#PageIndex t2
ON			
		t.[ThreadID] = t2.[ThreadID]

JOIN	[dbo].mp_Forums f
ON t.ForumID = f.ItemID

LEFT OUTER JOIN	[dbo].mp_Users u
ON t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users s
ON t.StartedByUserID = s.UserID
		
WHERE
		t2.IndexID > @PageLowerBound 
		AND t2.IndexID < @PageUpperBound
		
ORDER BY t2.IndexID

DROP TABLE #PageIndex
GO
/****** Object:  Table [dbo].[mp_ForumPosts]    Script Date: 11/10/2008 13:12:21 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ForumPosts](
	[PostID] [int] IDENTITY(1,1) NOT NULL,
	[ThreadID] [int] NOT NULL,
	[ThreadSequence] [int] NOT NULL CONSTRAINT [DF_mp_ForumPosts_ThreadSequence]  DEFAULT ((1)),
	[Subject] [nvarchar](255) NOT NULL,
	[PostDate] [datetime] NOT NULL CONSTRAINT [DF_mp_ForumPosts_PostDate]  DEFAULT (getdate()),
	[Approved] [bit] NOT NULL CONSTRAINT [DF_mp_ForumPosts_Approved]  DEFAULT ((0)),
	[UserID] [int] NOT NULL CONSTRAINT [DF_mp_ForumPosts_UserID]  DEFAULT ((-1)),
	[SortOrder] [int] NOT NULL CONSTRAINT [DF_mp_ForumPosts_SortOrder]  DEFAULT ((100)),
	[Post] [ntext] NOT NULL,
 CONSTRAINT [PK_mp_ForumPosts] PRIMARY KEY CLUSTERED 
(
	[PostID] ASC
) ON [PRIMARY]
) ON [PRIMARY] TEXTIMAGE_ON [PRIMARY]
GO
/****** Object:  Table [dbo].[mp_ForumThreadSubscriptions]    Script Date: 11/10/2008 13:12:35 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ForumThreadSubscriptions](
	[ThreadSubscriptionID] [int] IDENTITY(1,1) NOT FOR REPLICATION NOT NULL,
	[ThreadID] [int] NOT NULL,
	[UserID] [int] NOT NULL,
	[SubscribeDate] [datetime] NOT NULL CONSTRAINT [DF_mp_ForumThreadSubscriptions_SubscribeDate]  DEFAULT (getdate()),
	[UnSubscribeDate] [datetime] NULL,
 CONSTRAINT [PK_mp_ForumThreadSubscriptions] PRIMARY KEY CLUSTERED 
(
	[ThreadSubscriptionID] ASC
) ON [PRIMARY]
) ON [PRIMARY]
GO
/****** Object:  Table [dbo].[mp_ForumThreads]    Script Date: 11/10/2008 13:12:33 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE TABLE [dbo].[mp_ForumThreads](
	[ThreadID] [int] IDENTITY(1,1) NOT NULL,
	[ForumID] [int] NOT NULL,
	[ThreadSubject] [nvarchar](255) NOT NULL,
	[ThreadDate] [datetime] NOT NULL CONSTRAINT [DF_mp_ForumThreads_ThreadDate]  DEFAULT (getdate()),
	[TotalViews] [int] NOT NULL CONSTRAINT [DF_mp_ForumThreads_TotalViews]  DEFAULT ((0)),
	[TotalReplies] [int] NOT NULL CONSTRAINT [DF_mp_ForumThreads_TotalReplies]  DEFAULT ((0)),
	[SortOrder] [int] NOT NULL CONSTRAINT [DF_mp_ForumThreads_SortOrder]  DEFAULT ((1000)),
	[IsLocked] [bit] NOT NULL CONSTRAINT [DF_mp_ForumThreads_IsLocked]  DEFAULT ((0)),
	[ForumSequence] [int] NOT NULL CONSTRAINT [DF_mp_ForumThreads_ForumSequence]  DEFAULT ((1)),
	[MostRecentPostDate] [datetime] NULL CONSTRAINT [DF_mp_ForumThreads_MostRecentPostDate]  DEFAULT (getdate()),
	[MostRecentPostUserID] [int] NULL,
	[StartedByUserID] [int] NOT NULL,
 CONSTRAINT [PK_mp_ForumThreads] PRIMARY KEY CLUSTERED 
(
	[ThreadID] ASC
) ON [PRIMARY]
) ON [PRIMARY]
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectForRss]    Script Date: 11/10/2008 13:12:08 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectForRss]

/*
Author:				Joseph Hill
Created:			11/01/2005
Last Modified:			11/04/2005

A list of all forums posts (and associated data) filtered by the specified criteria

MaximumDays is the maximum age in days of the posts to return

*/
@SiteID				int,
@PageID				int,
@ModuleID			int,
@ItemID				int,
@ThreadID			int,
@MaximumDays			int


AS
SELECT		fp.*,
		ft.ThreadSubject,
		ft.ForumID,
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'PostAuthorTotalPosts' = up.TotalPosts,
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		[dbo].mp_ForumPosts fp

JOIN		[dbo].mp_ForumThreads ft
ON		fp.ThreadID = ft.ThreadID

JOIN		[dbo].mp_Forums f
ON		ft.ForumID = f.ItemID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID

JOIN	[dbo].mp_PageModules pm
ON	    m.ModuleID = pm.ModuleID

JOIN		[dbo].mp_Pages p
ON		pm.PageID = p.PageID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = fp.UserID

WHERE	p.SiteID = @SiteID
AND	(@PageID = -1 OR p.PageID = @PageID)
AND	(@ModuleID = -1 OR m.ModuleID = @ModuleID)
AND	(@ItemID = -1 OR f.ItemID = @ItemID)
AND	(@ThreadID = -1 OR ft.ThreadID = @ThreadID)
AND	(@MaximumDays = -1 OR datediff(dd, getdate(), fp.PostDate) <= @MaximumDays)

ORDER BY	fp.PostDate DESC
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectByPage]    Script Date: 11/10/2008 13:12:16 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByPage]

/*
Author:			Joe Audettte
Created:		7/2/2005
Last Modified:		7/2/2005

*/


@SiteID		int,
@PageID		int

AS
SELECT  	fp.*,
		f.ModuleID,
		f.ItemID,
		m.ModuleTitle,
		md.FeatureName

FROM		[dbo].mp_ForumPosts fp

JOIN		[dbo].mp_ForumThreads ft
ON		fp.ThreadID = ft.ThreadID

JOIN		[dbo].mp_Forums f
ON		f.ItemID = ft.ForumID

JOIN		[dbo].mp_Modules m
ON		f.ModuleID = m.ModuleID

JOIN		[dbo].mp_ModuleDefinitions md
ON		m.ModuleDefID = md.ModuleDefID

JOIN		[dbo].mp_PageModules pm
ON			pm.ModuleID = m.ModuleID

JOIN		[dbo].mp_Pages p
ON		p.PageID = pm.PageID

WHERE	p.SiteID = @SiteID
		AND pm.PageID = @PageID
		AND fp.Approved = 1
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_Delete]    Script Date: 11/10/2008 13:12:10 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_Forums_Delete]

/*
Author:				
Created:			6/27/2006
Last Modified:		12/11/2006

*/

@ItemID			int

AS

DELETE FROM [dbo].mp_ForumPosts

WHERE ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads
					WHERE ForumID = @ItemID)

DELETE FROM [dbo].mp_ForumThreadSubscriptions

WHERE	ThreadID IN (SELECT ThreadID FROM [dbo].mp_ForumThreads
					WHERE ForumID = @ItemID)

DELETE
FROM [dbo].mp_ForumThreads
WHERE ForumID = @ItemID

DELETE
FROM [dbo].mp_ForumSubscriptions
WHERE ForumID = @ItemID


DELETE
FROM			[dbo].mp_Forums 

WHERE		ItemID = @ItemID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_CountByUser]    Script Date: 11/10/2008 13:12:14 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_CountByUser]

/*
Author:				
Created:			2008-03-18
Last Modified:		2008-03-18

*/


@UserID		int

AS

SELECT	COUNT(*)

FROM		[dbo].mp_ForumThreads

WHERE ThreadID IN (Select DISTINCT ThreadID FROM [dbo].mp_ForumPosts WHERE [dbo].mp_ForumPosts.UserID = @UserID)
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_UpdateViewStats]    Script Date: 11/10/2008 13:12:17 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_UpdateViewStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ThreadID			int



AS


UPDATE		[dbo].mp_ForumThreads

SET		
			TotalViews = TotalViews + 1


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_IncrementReplyCount]    Script Date: 11/10/2008 13:12:14 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_IncrementReplyCount]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2006

*/

@ThreadID			int,
@MostRecentPostUserID	int,
@MostRecentPostDate datetime



AS
UPDATE		[dbo].mp_ForumThreads

SET			MostRecentPostUserID = @MostRecentPostUserID,
			TotalReplies = TotalReplies + 1,
			MostRecentPostDate = @MostRecentPostDate


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_Update]    Script Date: 11/10/2008 13:12:17 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Update]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ThreadID			int,
@ForumID			int,
@ThreadSubject		nvarchar(255),
@SortOrder			int,
@IsLocked			bit


AS


UPDATE		[dbo].mp_ForumThreads

SET			ForumID = @ForumID,
			ThreadSubject = @ThreadSubject,
			SortOrder = @SortOrder,
			IsLocked = @IsLocked


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_UpdateForumSequence]    Script Date: 11/10/2008 13:12:17 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_UpdateForumSequence]

/*
Author:			
Created:		2007-06-07
Last Modified:	2007-06-07

*/

@ThreadID			int,
@ForumSequence			int


AS


UPDATE		[dbo].mp_ForumThreads

SET			ForumSequence = @ForumSequence


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_Delete]    Script Date: 11/10/2008 13:12:14 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Delete]

/*
Author:			
Created:		11/28/2004
Last Modified:	8/14/2006

*/

@ThreadID			int

AS

DELETE FROM [dbo].mp_ForumThreadSubscriptions

WHERE	ThreadID = @ThreadID

DELETE FROM [dbo].mp_ForumPosts

WHERE	ThreadID = @ThreadID

DELETE FROM 		[dbo].mp_ForumThreads


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_DecrementReplyCount]    Script Date: 11/10/2008 13:12:14 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_DecrementReplyCount]

/*
Author:			
Created:		2/19/2005
Last Modified:		2/19/2005

*/

@ThreadID			int


AS

DECLARE @MostRecentPostUserID int
DECLARE @MostRecentPostDate datetime
 
SELECT TOP 1  @MostRecentPostUserID = UserID,
		@MostRecentPostDate = PostDate

FROM [dbo].mp_ForumPosts 
WHERE ThreadID = @ThreadID 
ORDER BY PostID DESC


UPDATE		[dbo].mp_ForumThreads

SET			MostRecentPostUserID = @MostRecentPostUserID,
			TotalReplies = TotalReplies - 1,
			MostRecentPostDate = @MostRecentPostDate


WHERE		ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectAllByThread]    Script Date: 11/10/2008 13:12:07 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThread]

/*
Author:				
Created:			2/19/2005
Last Modified:			7/18/2007

An approach to paging grids in the database, hopefully more efficient than bringing back a zillion rows and
paging with a DataGrid using viewstate

ThreadSequence is the integer order that postss were created within the thread

*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		'MostRecentPostUser' = COALESCE(u.[Name],'Guest'),
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'PostAuthorTotalPosts' = COALESCE(up.TotalPosts, 0),
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectAllByThreadRevereSorted]    Script Date: 11/10/2008 13:12:08 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectAllByThreadRevereSorted]

/*
Author:				
Created:			2007-07-17
Last Modified:		2008-09-16

*/

@ThreadID			int


AS


SELECT	p.*,
		ft.ForumID,
		'MostRecentPostUser' = COALESCE(u.[Name],'Guest'),
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'AuthorEmail' = COALESCE(up.[Email], ''),
		'PostAuthorTotalPosts' = COALESCE(up.TotalPosts, 0),
		'Trusted' = COALESCE(up.Trusted, 0),
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		

ORDER BY	p.ThreadSequence DESC
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectByThread]    Script Date: 11/10/2008 13:12:08 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectByThread]

/*
Author:				
Created:			2004-09-14
Last Modified:		2008-08-13

An approach to paging grids in the database, hopefully more efficient than bringing back a zillion rows and
paging with a DataGrid using viewstate

ThreadSequence is the integer order that postss were created within the thread

*/

@ThreadID			int,
@PageNumber			int

AS

DECLARE @PostsPerPage	int
--DECLARE @TotalPosts		int
DECLARE @CurrentPageMaxThreadSequence	int

DECLARE @BeginSequence int
DECLARE @EndSequence int



SELECT	@PostsPerPage = f.PostsPerPage
		--@TotalPosts = ft.TotalReplies

FROM		[dbo].mp_ForumThreads ft

JOIN		[dbo].mp_Forums f
ON		ft.ForumID = f.ItemID

WHERE	ft.ThreadID = @ThreadID

SET @CurrentPageMaxThreadSequence = (@PostsPerPage * @PageNumber) 
IF @CurrentPageMaxThreadSequence > @PostsPerPage
	BEGIN
		SET @BeginSequence = @CurrentPageMaxThreadSequence  - @PostsPerPage + 1
	END
ELSE
	BEGIN
		SET @BeginSequence = 1
	END

SET @EndSequence = @BeginSequence + @PostsPerPage  -1



SELECT	p.*,
		ft.ForumID,
		'MostRecentPostUser' = COALESCE(u.[Name],'Guest'),
		'StartedBy' = COALESCE(s.[Name],'Guest'),
		'PostAuthor' = COALESCE(up.[Name], 'Guest'),
		'AuthorEmail' = COALESCE(up.[Email], ''),
		'PostAuthorTotalPosts' = COALESCE(up.TotalPosts, 0),
		'Trusted' = COALESCE(up.Trusted, 0),
		'PostAuthorAvatar' = up.AvatarUrl,
		'PostAuthorWebSiteUrl' = up.WebSiteURL,
		'PostAuthorSignature' = up.Signature


FROM		[dbo].mp_ForumPosts p

JOIN		[dbo].mp_ForumThreads ft
ON		p.ThreadID = ft.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		ft.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		ft.StartedByUserID = s.UserID

LEFT OUTER JOIN		[dbo].mp_Users up
ON		up.UserID = p.UserID

WHERE	ft.ThreadID = @ThreadID
		AND p.ThreadSequence >= @BeginSequence
		AND  p.ThreadSequence <= @EndSequence

ORDER BY	p.SortOrder, p.PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_RecalculatePostStats]    Script Date: 11/10/2008 13:12:11 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_RecalculatePostStats]

/*
Author:			
Created:		9/11/2005
Last Modified:		9/11/2005

based ont he pgsql version by Dean Brettle

*/

@ForumID		int


AS

DECLARE @RowsAffected		int
DECLARE @MostRecentPostDate	datetime
DECLARE @MostRecentPostUserID	int
DECLARE @PostCount			int

SET @RowsAffected = 0

SELECT TOP 1	@MostRecentPostDate = MostRecentPostDate,
		@MostRecentPostUserID = MostRecentPostUserID

FROM		[dbo].mp_ForumThreads

WHERE	ForumID = @ForumID

ORDER BY	MostRecentPostDate DESC

SET @PostCount = COALESCE(
				(	SELECT 	SUM(TotalReplies) + COUNT(*)
					FROM		[dbo].mp_ForumThreads
					WHERE	ForumID = @ForumID

				),
				0
				)

UPDATE 	[dbo].mp_Forums
SET		MostRecentPostDate = @MostRecentPostDate,
		MostRecentPostUserID = @MostRecentPostUserID,
		PostCount = @PostCount

WHERE	ItemID = @ForumID

SET @RowsAffected = @@ROWCOUNT


SELECT @RowsAffected
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectByForum]    Script Date: 11/10/2008 13:12:15 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByForum]

/*
Author:			
Created:		9/14/2004
Last Modified:		2/16/2005

*/


@ForumID			int,
@PageNumber			int

AS

DECLARE @ThreadsPerPage	int
DECLARE @CurrentPageMaxForumSequence	int
DECLARE @BeginSequence int
DECLARE @EndSequence int

SELECT	@ThreadsPerPage = ThreadsPerPage
		
FROM		[dbo].mp_Forums

WHERE	ItemID = @ForumID

SET @CurrentPageMaxForumSequence = (@ThreadsPerPage * @PageNumber)

IF @CurrentPageMaxForumSequence > @ThreadsPerPage + 1
	BEGIN
		SET @BeginSequence = @CurrentPageMaxForumSequence  
- @ThreadsPerPage + 1
	END
ELSE
	BEGIN
		SET @BeginSequence = 1
	END

SET @EndSequence = @BeginSequence + @ThreadsPerPage  -1

SELECT	t.*,
		'MostRecentPostUser' = COALESCE(u.[Name], 'Guest'),
		'StartedBy' = s.[Name]


FROM		[dbo].mp_ForumThreads t

LEFT OUTER JOIN		[dbo].mp_Users u
ON		t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		t.StartedByUserID = s.UserID

WHERE	t.ForumID = @ForumID
		AND t.ForumSequence 
Between @BeginSequence 
AND @EndSequence

ORDER BY	t.SortOrder, t.ThreadID DESC
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectOne]    Script Date: 11/10/2008 13:12:16 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectOne]

/*
Author:			
Created:		9/19/2004
Last Modified:		2/16/2005

*/

@ThreadID		int


AS

SELECT		t.*,
			'MostRecentPostUser' = COALESCE(u.[Name], 'Guest'),
			'StartedBy' = COALESCE(s.[Name], 'Guest'),
			f.PostsPerPage


FROM			[dbo].mp_ForumThreads t

LEFT OUTER JOIN	[dbo].mp_Users u
ON			t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users s
ON			t.StartedByUserID = s.UserID

JOIN			[dbo].mp_Forums f
ON			f.ItemID = t.ForumID

WHERE		t.ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectByForumDesc]    Script Date: 11/10/2008 13:12:15 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByForumDesc]

/*
Author:			
Created:		9/14/2004
Last Modified:		9/25/2004

*/


@ForumID			int,
@PageNumber			int

AS

DECLARE @ThreadsPerPage	int
DECLARE @TotalThreads	int
DECLARE @BeginSequence int
DECLARE @EndSequence int

SELECT	@ThreadsPerPage = ThreadsPerPage,
		@TotalThreads = ThreadCount
FROM		[dbo].mp_Forums
WHERE	ItemID = @ForumID


SET @BeginSequence = @TotalThreads - (@ThreadsPerPage * @PageNumber) + 1
SET @EndSequence = @BeginSequence + @ThreadsPerPage  -1

SELECT	t.*,
		'MostRecentPostUser' = u.[Name],
		'StartedBy' = s.[Name]


FROM		[dbo].mp_ForumThreads t

LEFT OUTER JOIN		[dbo].mp_Users u
ON		t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		t.StartedByUserID = s.UserID

WHERE	t.ForumID = @ForumID
		AND t.ForumSequence 
Between @BeginSequence 
AND @EndSequence

ORDER BY	t.SortOrder, t.ThreadID DESC
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_Insert]    Script Date: 11/10/2008 13:12:15 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_Insert]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2007

*/

@ForumID			int,
@ThreadSubject		nvarchar(255),
@SortOrder			int,
@IsLocked			bit,
@StartedByUserID		int,
@ThreadDate		datetime


AS
DECLARE @ThreadID int
DECLARE @ForumSequence int
SET @ForumSequence = (SELECT COALESCE(Max(ForumSequence) + 1,1) FROM [dbo].mp_ForumThreads WHERE ForumID = @ForumID)

INSERT INTO		[dbo].mp_ForumThreads
(
			ForumID,
			ThreadSubject,
			SortOrder,
			ForumSequence,
			IsLocked,
			StartedByUserID,
			ThreadDate,
			MostRecentPostUserID,
			MostRecentPostDate

)

VALUES
(
			
			@ForumID,
			@ThreadSubject,
			@SortOrder,
			@ForumSequence,
			@IsLocked,
			@StartedByUserID,
			@ThreadDate,
			@StartedByUserID,
			@ThreadDate


)

SELECT @ThreadID = @@IDENTITY 


INSERT INTO [dbo].mp_ForumThreadSubscriptions (ThreadID, UserID)
	SELECT @ThreadID, UserID FROM [dbo].mp_ForumSubscriptions fs 
		WHERE fs.ForumID = @ForumID AND fs.SubscribeDate IS NOT NULL 
				AND fs.UnSubscribeDate IS NULL

SELECT @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_Delete]    Script Date: 11/10/2008 13:12:07 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Delete]

/*
Author:			
Created:		11/6/2004
Last Modified:		11/6/2004

*/

@PostID		int


AS

DELETE FROM [dbo].mp_ForumPosts

WHERE PostID = @PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_UpdateSequence]    Script Date: 11/10/2008 13:12:09 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_UpdateSequence]

/*
Author:			
Created:		2007-06-07
Last Modified:	2007-06-07

*/

@PostID			int,
@ThreadSequence			int


AS

UPDATE		[dbo].mp_ForumPosts

SET			ThreadSequence = @ThreadSequence


WHERE		PostID = @PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_Update]    Script Date: 11/10/2008 13:12:09 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Update]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@PostID			int,
@Subject			nvarchar(255),
@Post				ntext,
@SortOrder			int,
@Approved			bit

AS

UPDATE		[dbo].mp_ForumPosts

SET			Subject = @Subject,
			Post = @Post,
			SortOrder = @SortOrder,
			Approved = @Approved


WHERE		PostID = @PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_Insert]    Script Date: 11/10/2008 13:12:07 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_Insert]

/*
Author:			
Created:		9/19/2004
Last Modified:		1/14/2007

*/


@ThreadID			int,
@Subject			nvarchar(255),
@Post				ntext,
@Approved			bit,
@UserID			int,
@PostDate		datetime



AS
DECLARE @ThreadSequence int
SET @ThreadSequence = (SELECT COALESCE(Max(ThreadSequence) + 1,1) FROM [dbo].mp_ForumPosts WHERE ThreadID = @ThreadID)



INSERT INTO		[dbo].mp_ForumPosts
(
			ThreadID,
			Subject,
			Post,
			Approved,
			UserID,
			ThreadSequence,
			PostDate
)

VALUES
(
			@ThreadID,
			@Subject,
			@Post,
			@Approved,
			@UserID,
			@ThreadSequence,
			@PostDate

)

SELECT  @@IDENTITY As PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectOne]    Script Date: 11/10/2008 13:12:08 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectOne]

/*
Author:				
Created:			10/17/2004
Last Modified:			10/17/2004

*/

@PostID		int

AS


SELECT	fp.*

FROM		[dbo].mp_ForumPosts fp

WHERE	fp.PostID = @PostID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_SelectSortedThread]    Script Date: 11/10/2008 13:12:09 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_SelectSortedThread]

/*
Author:				
Created:			2007-06-08
Last Modified:		2007-06-08


*/

@ThreadID			int

AS

SELECT	*
		
FROM		[dbo].mp_ForumPosts


WHERE	ThreadID = @ThreadID
		

ORDER BY	ThreadSequence
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumPosts_CountByThread]    Script Date: 11/10/2008 13:12:07 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumPosts_CountByThread]

/*
Author:				
Created:			11/28/2004
Last Modified:			11/28/2004

*/


@ThreadID		int



AS


SELECT	COUNT(*)

FROM		[dbo].mp_ForumPosts

WHERE	ThreadID = @ThreadID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreadSubscriptions_Exists]    Script Date: 11/10/2008 13:12:18 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_Exists]

/*
Author:			
Created:		2007/05/24
Last Modified:	2007/05/24

*/
    
@ThreadID  	int,
@UserID		int

AS
IF EXISTS (	SELECT 	ThreadSubscriptionID
		FROM		[dbo].mp_ForumThreadSubscriptions
		WHERE	ThreadID = @ThreadID
				AND UserID = @UserID
				AND UnSubscribeDate is null )
SELECT 1
ELSE
SELECT 0
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreadSubscriptions_UnsubscribeThread]    Script Date: 11/10/2008 13:12:18 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_UnsubscribeThread]

/*
Author:				
Created:			10/14/2004
Last Modified:			10/14/2004

*/

@ThreadID		int,
@UserID		int

AS

UPDATE		[dbo].mp_ForumThreadSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		ThreadID = @ThreadID
			AND UserID = @UserID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreadSubscribers_SelectByThread]    Script Date: 11/10/2008 13:12:17 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscribers_SelectByThread]

/*
Author:				
Created:			10/13/2004
Last Modified:			10/13/2004

*/


@ThreadID		int,
@CurrentPostUserID 	int


AS

SELECT		u.Email


FROM			[dbo].mp_Users u

JOIN			[dbo].mp_ForumThreadSubscriptions s
ON			u.UserID = s.UserID

WHERE		s.ThreadID = @ThreadID
			AND s.UnSubscribeDate IS NULL
			AND u.UserID <> @CurrentPostUserID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreadSubscriptions_UnsubscribeAllThreads]    Script Date: 11/10/2008 13:12:18 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_UnsubscribeAllThreads]

/*
Author:				
Created:			10/14/2004
Last Modified:			10/14/2004

*/

@UserID		int

AS

UPDATE		[dbo].mp_ForumThreadSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		
			UserID = @UserID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreadSubscriptions_Insert]    Script Date: 11/10/2008 13:12:18 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreadSubscriptions_Insert]

/*
Author:			
Created:		10/14/2004
Last Modified:		1/21/2006

*/

@ThreadID		int,
@UserID		int


AS

IF EXISTS (SELECT UserID FROM mp_ForumThreadSubscriptions WHERE ThreadID = @ThreadID AND UserID = @UserID)
BEGIN
	UPDATE 	[dbo].mp_ForumThreadSubscriptions

	SET		SubscribeDate = GetDate(),
			UnSubscribeDate = Null
	

	WHERE 	ThreadID = @ThreadID AND UserID = @UserID

END

ELSE

BEGIN

	INSERT INTO	[dbo].mp_ForumThreadSubscriptions
	(
			ThreadID,
			UserID
	)
	VALUES
	(
			@ThreadID,
			@UserID
	)

END
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_DecrementPostCount]    Script Date: 11/10/2008 13:12:09 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_DecrementPostCount]

/*
Author:				
Created:			11/6/2004
Last Modified:			11/6/2004

*/

@ForumID		int

AS


UPDATE [dbo].mp_Forums

SET PostCount = PostCount - 1

WHERE ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_Insert]    Script Date: 11/10/2008 13:12:11 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_Forums_Insert]

/*
Author:				
Created:			9/12/2004
Last Modified:			10/20/2004

*/

@ModuleID			int,
@UserID			int,
@Title          			nvarchar(100),
@Description    			ntext,
@IsModerated			bit,
@IsActive			bit,
@SortOrder			int,
@PostsPerPage			int,
@ThreadsPerPage		int,
@AllowAnonymousPosts		bit



AS

INSERT INTO			[dbo].mp_Forums
(
				ModuleID,
				CreatedBy,
				Title,
				[Description],
				IsModerated,
				IsActive,
				SortOrder,
				PostsPerPage,
				ThreadsPerPage,
				AllowAnonymousPosts

)

VALUES
(
				@ModuleID,
				@UserID,
				@Title,
				@Description,
				@IsModerated,
				@IsActive,
				@SortOrder,
				@PostsPerPage,
				@ThreadsPerPage,
				@AllowAnonymousPosts

)

SELECT @@IDENTITY As ItemID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_Update]    Script Date: 11/10/2008 13:12:12 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_Forums_Update]

/*
Author:			
Created:		9/12/2004
Last Modified:		10/20/2004

*/

@ItemID			int,
@Title          			nvarchar(100),
@Description    			ntext,
@IsModerated			bit,
@IsActive			bit,
@SortOrder			int,
@PostsPerPage			int,
@ThreadsPerPage		int,
@AllowAnonymousPosts		bit



AS


UPDATE		[dbo].mp_Forums

SET			Title = @Title,
			[Description] = @Description,
			IsModerated = @IsModerated,
			IsActive = @IsActive,
			SortOrder = @SortOrder,
			PostsPerPage = @PostsPerPage,
			ThreadsPerPage = @ThreadsPerPage,
			AllowAnonymousPosts = @AllowAnonymousPosts



WHERE		ItemID = @ItemID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_Select]    Script Date: 11/10/2008 13:12:12 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_Forums_Select]


/*
Author:			
Created:		9/12/2004
Last Modified:		9/12/2004

*/

@ModuleID			int,
@UserID				int

AS


SELECT		f.*,
			'MostRecentPostUser' = u.[Name],
			'Subscribed' = CASE WHEN s.[SubscribeDate] IS NOT NULL and s.[UnSubscribeDate] IS NULL THEN 1
					Else 0
					End

FROM			[dbo].mp_Forums f

LEFT OUTER JOIN	[dbo].mp_Users u
ON			f.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN [dbo].mp_ForumSubscriptions s
on			f.ItemID = s.ForumID and s.UserID = @UserID

WHERE		f.ModuleID	= @ModuleID
			AND f.IsActive = 1

ORDER BY		f.SortOrder, f.ItemID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_IncrementThreadCount]    Script Date: 11/10/2008 13:12:10 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementThreadCount]

/*
Author:			
Created:		11/28/2004
Last Modified:		11/28/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount + 1

WHERE		ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_DecrementThreadCount]    Script Date: 11/10/2008 13:12:10 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_DecrementThreadCount]

/*
Author:			
Created:		11/28/2004
Last Modified:		11/28/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount - 1

WHERE		ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_IncrementPostCount]    Script Date: 11/10/2008 13:12:10 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementPostCount]

/*
Author:				
Created:			11/6/2004
Last Modified:			1/14/2007

*/

@ForumID			int,
@MostRecentPostUserID	int,
@MostRecentPostDate datetime

AS
UPDATE 	[dbo].mp_Forums

SET 		MostRecentPostDate = @MostRecentPostDate,
		MostRecentPostUserID = @MostRecentPostUserID,
 		PostCount = PostCount + 1

WHERE 	ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_IncrementPostCountOnly]    Script Date: 11/10/2008 13:12:10 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_IncrementPostCountOnly]

/*
Author:				
Created:			9/10/2005
Last Modified:			9/10/2005

*/

@ForumID			int


AS


UPDATE 	[dbo].mp_Forums

SET 		
 		PostCount = PostCount + 1

WHERE 	ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_UpdateThreadStats]    Script Date: 11/10/2008 13:12:13 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_UpdateThreadStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ForumID			int

AS

UPDATE		[dbo].mp_Forums

SET			ThreadCount = ThreadCount + 1

WHERE		ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_UpdatePostStats]    Script Date: 11/10/2008 13:12:13 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_Forums_UpdatePostStats]

/*
Author:			
Created:		9/19/2004
Last Modified:		9/19/2004

*/

@ForumID			int,
@MostRecentPostUserID	int

AS

UPDATE	[dbo].mp_Forums

SET		MostRecentPostDate = GetDate(),
		MostRecentPostUserID = @MostRecentPostUserID,
		PostCount = PostCount + 1

WHERE	ItemID = @ForumID
GO
/****** Object:  StoredProcedure [dbo].[mp_Forums_SelectOne]    Script Date: 11/10/2008 13:12:12 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[mp_Forums_SelectOne]

/*
Author:				
Created:			9/12/2004
Last Modified:			9/12/2004

*/

@ItemID			int

AS

SELECT		f.*,
			'CreatedByUser' = u.[Name],
			'MostRecentPostUser' = up.[Name]

FROM			[dbo].mp_Forums f

LEFT OUTER JOIN	[dbo].mp_Users u
ON			f.CreatedBy = u.UserID

LEFT OUTER JOIN	[dbo].mp_Users up
ON			f.MostRecentPostUserID = up.UserID

WHERE		f.ItemID = @ItemID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumThreads_SelectByForumDesc_v2]    Script Date: 11/10/2008 13:12:16 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumThreads_SelectByForumDesc_v2]

/*
Author:			
Created:		9/14/2004
Last Modified:		9/25/2004

*/


@ForumID			int,
@PageNumber			int

AS

DECLARE @ThreadsPerPage	int
DECLARE @TotalThreads	int
DECLARE @BeginSequence int
DECLARE @EndSequence int

SELECT	@ThreadsPerPage = ThreadsPerPage,
		@TotalThreads = ThreadCount
FROM		[dbo].mp_Forums
WHERE	ItemID = @ForumID


SET @BeginSequence = @TotalThreads - (@ThreadsPerPage * @PageNumber) + 1
SET @EndSequence = @BeginSequence + @ThreadsPerPage  -1

CREATE TABLE #PageIndex 
(
	IndexID int IDENTITY (1, 1) NOT NULL,
	ThreadID int
	
)

INSERT INTO #PageIndex (ThreadID)


SELECT	t.ThreadID
FROM		[dbo].mp_ForumThreads t
WHERE	t.ForumID = @ForumID	
ORDER BY	t.MostRecentPostDate 


SELECT	t.*,
		'MostRecentPostUser' = u.[Name],
		'StartedBy' = s.[Name]


FROM		[dbo].mp_ForumThreads t

JOIN		#PageIndex p
ON		p.ThreadID = t.ThreadID

LEFT OUTER JOIN		[dbo].mp_Users u
ON		t.MostRecentPostUserID = u.UserID

LEFT OUTER JOIN		[dbo].mp_Users s
ON		t.StartedByUserID = s.UserID

WHERE	t.ForumID = @ForumID
		AND p.IndexID
Between @BeginSequence 
AND @EndSequence

ORDER BY	p.IndexID DESC

DROP TABLE #PageIndex
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumSubscriptions_Insert]    Script Date: 11/10/2008 13:12:13 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Insert]

/*
Author:			Dean Brettle
Created:		9/11/2005
Last Modified:		9/11/2005

*/

@ForumID		int,
@UserID		int


AS

IF EXISTS (SELECT UserID FROM mp_ForumSubscriptions WHERE ForumID = @ForumID AND UserID = @UserID)
BEGIN
	UPDATE 	[dbo].mp_ForumSubscriptions

	SET		SubscribeDate = GetDate(),
			UnSubscribeDate = Null
	

	WHERE 	ForumID = @ForumID AND UserID = @UserID

END

ELSE

BEGIN

	INSERT INTO	[dbo].mp_ForumSubscriptions
	(
			ForumID,
			UserID
	)
	VALUES
	(
			@ForumID,
			@UserID
	)

END
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumSubscriptions_Unsubscribe]    Script Date: 11/10/2008 13:12:14 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Unsubscribe]

/*
Author:				Dean Brettle
Created:			9/11/2005
Last Modified:			9/11/2005

*/

@ForumID		int,
@UserID		int

AS

UPDATE		[dbo].mp_ForumSubscriptions

SET			UnSubscribeDate = GetDate()

WHERE		ForumID = @ForumID
			AND UserID = @UserID
GO
/****** Object:  StoredProcedure [dbo].[mp_ForumSubscriptions_Exists]    Script Date: 11/10/2008 13:12:13 ******/
SET ANSI_NULLS OFF
GO
SET QUOTED_IDENTIFIER OFF
GO
CREATE PROCEDURE [dbo].[mp_ForumSubscriptions_Exists]

/*
Author:			
Created:		2007/05/24
Last Modified:	2007/05/24

*/
    
@ForumID  	int,
@UserID		int

AS
IF EXISTS (	SELECT 	SubscriptionID
		FROM		[dbo].mp_ForumSubscriptions
		WHERE	ForumID = @ForumID
				AND UserID = @UserID
				AND UnSubscribeDate is null )
SELECT 1
ELSE
SELECT 0
GO
/****** Object:  ForeignKey [FK_mp_ForumPosts_mp_ForumThreads]    Script Date: 11/10/2008 13:12:22 ******/
ALTER TABLE [dbo].[mp_ForumPosts]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumPosts_mp_ForumThreads] FOREIGN KEY([ThreadID])
REFERENCES [dbo].[mp_ForumThreads] ([ThreadID])
GO
ALTER TABLE [dbo].[mp_ForumPosts] CHECK CONSTRAINT [FK_mp_ForumPosts_mp_ForumThreads]
GO
/****** Object:  ForeignKey [FK_mp_ForumThreads_mp_Forums]    Script Date: 11/10/2008 13:12:33 ******/
ALTER TABLE [dbo].[mp_ForumThreads]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumThreads_mp_Forums] FOREIGN KEY([ForumID])
REFERENCES [dbo].[mp_Forums] ([ItemID])
GO
ALTER TABLE [dbo].[mp_ForumThreads] CHECK CONSTRAINT [FK_mp_ForumThreads_mp_Forums]
GO
/****** Object:  ForeignKey [FK_mp_ForumThreadSubscriptions_mp_ForumThreads]    Script Date: 11/10/2008 13:12:35 ******/
ALTER TABLE [dbo].[mp_ForumThreadSubscriptions]  WITH CHECK ADD  CONSTRAINT [FK_mp_ForumThreadSubscriptions_mp_ForumThreads] FOREIGN KEY([ThreadID])
REFERENCES [dbo].[mp_ForumThreads] ([ThreadID])
GO
ALTER TABLE [dbo].[mp_ForumThreadSubscriptions] CHECK CONSTRAINT [FK_mp_ForumThreadSubscriptions_mp_ForumThreads]
GO
